
/*

DROP TABLE IF EXISTS `mission_board`;
CREATE TABLE IF NOT EXISTS `mission_board` (
	`id` int(11) unsigned NOT NULL,
	`title` varchar(30) NOT NULL default '',
	`desc` varchar(255) NOT NULL default '',
	`mob_list` varchar(50) NOT NULL default '',
	`mob_qty` varchar(50) NOT NULL default '',
	`item_list` varchar(50) NOT NULL default '',
	`item_qty` varchar(50) NOT NULL default '',
	`class_limitation` int(11) unsigned NOT NULL default '0',
	`class_branch` int(11) unsigned NOT NULL default '0',
	`min_lv` smallint(6) unsigned NOT NULL default '1',
	`max_lv` smallint(6) unsigned NOT NULL default '99',
	`repeat` smallint(6) unsigned NOT NULL default '0',
	`duration` int(11) unsigned NOT NULL default '0',
	`reward_list` varchar(50) NOT NULL default '',
	`reward_qty` varchar(50) NOT NULL default '',
	`base_exp` int(11) unsigned NOT NULL default '0',
	`job_exp` int(11) unsigned NOT NULL default '0',
	`zeny` int(11) unsigned NOT NULL default '0',
	`cash` int(11) unsigned NOT NULL default '0',
	`aid` int(11) unsigned NOT NULL default '0',
	`name` varchar(30) NOT NULL default '',
	`time_update` datetime NOT NULL default '0000-00-00 00:00:00',
	`npc_id` varchar(255) NOT NULL default '',
	`redo_delay` smallint(6) unsigned NOT NULL default '0',
	PRIMARY KEY  (`id`)
) ENGINE=MyISAM DEFAULT CHARSET=latin1;


DROP TABLE IF EXISTS `player_mission`;
CREATE TABLE IF NOT EXISTS `player_mission` (
	`id` int(11) unsigned NOT NULL,
	`mission_id` int(11) unsigned NOT NULL,
	`aid` int(11) unsigned NOT NULL default '0',
	`cid` int(11) unsigned NOT NULL default '0',
	`name` varchar(30) NOT NULL default '',
	`mob_hunt` varchar(50) NOT NULL default '',
	`expire` int(11) unsigned NOT NULL default '0',
	`starting` datetime NOT NULL default '0000-00-00 00:00:00',
	`completion` datetime NOT NULL default '0000-00-00 00:00:00',
	PRIMARY KEY  (`id`)
) ENGINE=MyISAM DEFAULT CHARSET=latin1;

*/



-	script	mission_board	-1,{
.@gm_level = getgmlevel();
.@npc_name$ = strnpcinfo(1);
.@mission_npc_num = atoi( strnpcinfo(2) );

// check npc if it's a valid npc with number 1 ~ 500
if( !.@mission_npc_num || .@mission_npc_num > 500 ){
	message strcharinfo(0),"<冒险家工会> 发现NPC发生错误, 错误的NPC名为: <'"+strnpcinfo(2)+"'>";
	disablenpc strnpcinfo(0);
	end;
}

// to assign offset index.
.@mission_npc_num--;
query_sql( "SELECT COUNT(`id`) FROM `mission_board` WHERE `npc_id` LIKE '%|"+.@mission_npc_num+"|%' ",.@mission_count );
query_sql( "SELECT `id`,`mission_id`,`mob_hunt`,`expire` FROM `player_mission` WHERE `mission_id` IN ( SELECT `id` FROM `mission_board` WHERE `npc_id` LIKE '%|"+.@mission_npc_num+"|%' ) AND `cid` = "+getcharid(0)+" AND `completion` = '0000-00-00 00:00:00' ",.@id,.@mission_id,.@mob_hunt$,.@expire );
.@current_mission_size = getarraysize( .@id );

mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
mes "-------------------------------";
mes "冒险家工会面板提供各种委托任务, 在米德加兹的所有冒险者都可以来接受委托.";
mes "  您最多同时可接受 ^FF0000"+.max_mission_per_char+"^000000 条委托任务.";
mes " ";
mes "                     - 冒险家工会- ";
next;
switch( select( ( .@current_mission_size )?"- 提交已完成的委托任务报告":"",
				( .@current_mission_size < .max_mission_per_char )?"- 接受一个新的委托任务":"",
				( .@current_mission_size )?"- 放弃已接的委托任务":"",
				( .@gm_level < .gm_level || !.@mission_count )?"":"^FF0000- [任务设置] 更新已发布的任务^000000",
				( .@gm_level < .gm_level )?"":"^FF0000- [任务设置] 新建一个委托任务^000000",
				( .@gm_level < .gm_level || !.@mission_count  )?"":"^FF0000- [任务设置] 删除现有的委托任务^000000" ) ){
	case 1:
		.@current_time = gettimetick(2);
		query_sql( "SELECT `id`,`title`,`min_lv`,`max_lv` FROM `mission_board` WHERE `npc_id` LIKE '%|"+.@mission_npc_num+"|%' AND `id` IN ( SELECT `mission_id` FROM `player_mission` WHERE `cid` = "+getcharid(0)+" AND `completion` = '0000-00-00 00:00:00' )",.@mission_id,.@title$,.@min_lv,.@max_lv );
		.@size = getarraysize( .@mission_id );
		for( .@i = 0; .@i < .@size; .@i++ ){
			if( .@expire[.@i] < .@current_time )
				.@submit_menu$ = .@submit_menu$ + "^FF0000[已过期]^000000";
			.@submit_menu$ = .@submit_menu$ + "["+.@min_lv[.@i]+"~"+.@max_lv[.@i]+"] "+.@title$[.@i] +":";
		}
		.@i = select( .@submit_menu$ ) - 1;
		
		if( .@expire[.@i] < .@current_time ){
			dispbottom "- 该委托任务已经超过时限 "+callsub( OnTime2Str,( gettimetick(2) + ( .@current_time - .@expire[.@i] ) ) )+" 了.";
			dispbottom "- 您只能放弃该委托任务了.";
			close;
		}

		// get mission data from sql
		query_sql( "SELECT * FROM `mission_board` WHERE `id` = "+.@mission_id[.@i]+" LIMIT 1",
			.@mission_id,
			.@title$,
			.@description$,
			.@mob_list$,
			.@mob_qty$,
			.@item_list$,
			.@item_qty$,
			.@base_job_bitmask,
			.@job_branch_bitmask,
			.@min_lv,
			.@max_lv,
			.@repeatable,
			.@timelimit,
			.@reward_list$,
			.@reward_qty$,
			.@baseexp,
			.@jobexp,
			.@zeny,
			.@cash,
			.@aid,
			.@name$,
			.@time_update$,
			.@npc_id$,
			.@redo_delay
		);
		
		// explode all saved strings to array value.
		.@monster_size = callsub( OnExplodeArray,.@mob_list$,.@monster_list,0 );
		if( .@monster_size )
			callsub( OnExplodeArray,.@mob_qty$,.@monster_qty,0 );
		
		.@item_size = callsub( OnExplodeArray,.@item_list$,.@item_list,0 );
		if( .@item_size )
			callsub( OnExplodeArray,.@item_qty$,.@item_qty,0 );
		
		.@reward_size = callsub( OnExplodeArray,.@reward_list$,.@reward_list,0 );
		if( .@reward_size )
			callsub( OnExplodeArray,.@reward_qty$,.@reward_qty,0 );
		
		.@selected_npc_size = callsub( OnExplodeArray,.@npc_id$,.@selected_npc_array$,1 );
	
		setarray .@level_range,.@min_lv,.@max_lv;
		
		// display the information of mission
		.@result = callsub( OnDisplayMissionInfo,
					.@mission_id,
					.@title$,
					.@description$,
					.@level_range,
					.@repeatable,
					.@expire[.@i],
					.@monster_list,
					.@monster_qty,
					.@item_list,
					.@item_qty,
					.@base_job_bitmask,
					.@job_branch_bitmask,
					.@baseexp,
					.@jobexp,
					.@cash,
					.@zeny,
					.@reward_list,
					.@reward_qty,
					.@selected_npc_array$,
					.@time_update$,
					.@redo_delay,
					1|2|4|8
				);
				
		// submit mission or not
		if( .@result ){
			message strcharinfo(0),"<冒险家工会> 无法提交该委托任务的完成报告.";
		}else{
			next;
			if( select( "- 提交已完成的委托任务报告","- 取消" ) == 1 ){
				for( .@ms = ( @ms_size - 1 ); .@ms >= 0; .@ms-- )
					if( @ms_list$[.@ms] == ""+.@mission_id ){
						mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
						query_sql( "UPDATE `player_mission` SET `completion` = NOW() WHERE `cid` = "+getcharid(0)+" AND `mission_id` = "+.@mission_id );
						mes "恭喜您! 任务完成.";
						@ms_size--;
						
						// clear requirement.
						setd( "@ms_"+.@mission_id+"_expire" ),0;
						deletearray getd( "@ms_"+.@mission_id+"_list" );
						deletearray getd( "@ms_"+.@mission_id+"_qty" );
						deletearray getd( "@ms_"+.@mission_id+"_hunt" );
						if( .@item_size )
							for( .@i = 0; .@i < .@item_size; .@i++ ){
								// debugmes getitemname( .@item_list[.@i] )+" - "+.@item_qty[.@i];
								delitem .@item_list[.@i],.@item_qty[.@i];
							}
						
						mes "获得了完成委托任务的奖励.";
						// rewards
						getexp .@baseexp,.@jobexp;
						if( .@reward_size )
							for( .@i = 0; .@i < .@reward_size; .@i++ )
								getitem .@reward_list[.@i],.@reward_qty[.@i];
						#CASHPOINTS += .@cash;
						Zeny += .@zeny;
						break;
					}
				mes "可能有一些程序错误,请联系[GM]安赫尔..";
			}
		}
		break;
	case 2:
		// get info from SQL.
		do{
			mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
			mes "-------------------------------";
			deletearray .@id;
			query_sql( "SELECT `id`,`title`,`min_lv`,`max_lv` FROM `mission_board` WHERE `npc_id` LIKE '%|"+.@mission_npc_num+"|%' AND `id` NOT IN ( SELECT `mission_id` FROM `player_mission` WHERE `cid` = "+getcharid(0)+" AND `completion` = '0000-00-00 00:00:00' ) LIMIT "+.max_page_size+" OFFSET "+.@offset,.@id,.@title$,.@min_lv,.@max_lv );
			.@offset += .max_page_size;
			.@size = getarraysize( .@id );
			if( !.@size ){
				mes "冒险家工会委托任务列表中暂时没有可选择的任务.";
				close;
				
			}else{
				mes "请在列表中选择一个想接受的委托任务.";
				.@mission_menu$ = "";
				for( .@i = 0; .@i < .@size; .@i++ )
					.@mission_menu$ = .@mission_menu$ + "["+.@min_lv[.@i]+"-"+.@max_lv[.@i]+"] "+.@title$[.@i] +":";
			}
			next;
			.@i = select( .@mission_menu$+ ( ( .@size < .max_page_size )?"":"- 下一页" ) ) - 1;
		}while( .@i == .@size );
		
		query_sql( "SELECT * FROM `mission_board` WHERE `id` = "+.@id[.@i]+" LIMIT 1",
			.@mission_id,
			.@title$,
			.@description$,
			.@mob_list$,
			.@mob_qty$,
			.@item_list$,
			.@item_qty$,
			.@base_job_bitmask,
			.@job_branch_bitmask,
			.@min_lv,
			.@max_lv,
			.@repeatable,
			.@timelimit,
			.@reward_list$,
			.@reward_qty$,
			.@baseexp,
			.@jobexp,
			.@zeny,
			.@cash,
			.@aid,
			.@name$,
			.@time_update$,
			.@npc_id$,
			.@redo_delay
		);
		
		// explode all saved strings to array value.
		.@monster_size = callsub( OnExplodeArray,.@mob_list$,.@monster_list,0 );
		.@monster_size = callsub( OnExplodeArray,.@mob_qty$,.@monster_qty,0 );
		
		.@item_size = callsub( OnExplodeArray,.@item_list$,.@item_list,0 );
		.@item_size = callsub( OnExplodeArray,.@item_qty$,.@item_qty,0 );
		
		.@reward_size = callsub( OnExplodeArray,.@reward_list$,.@reward_list,0 );
		.@reward_size = callsub( OnExplodeArray,.@reward_qty$,.@reward_qty,0 );
		
		.@selected_npc_size = callsub( OnExplodeArray,.@npc_id$,.@selected_npc_array$,1 );
	
		setarray .@level_range,.@min_lv,.@max_lv;
		
		// display the information of mission
		.@result = callsub( OnDisplayMissionInfo,
				.@mission_id,
				.@title$,
				.@description$,
				.@level_range,
				.@repeatable,
				( .@timelimit + gettimetick(2) ),
				.@monster_list,
				.@monster_qty,
				.@item_list,
				.@item_qty,
				.@base_job_bitmask,
				.@job_branch_bitmask,
				.@baseexp,
				.@jobexp,
				.@cash,
				.@zeny,
				.@reward_list,
				.@reward_qty,
				.@selected_npc_array$,
				.@time_update$,
				.@redo_delay,
				1|8
		);
		
		// check completed how many times.
		if( .@repeatable[.@i] ){
			query_sql( "SELECT COUNT(`id`),TIMESTAMPDIFF( HOUR,`completion`,NOW() ),DATE_ADD( `completion`, INTERVAL "+.@redo_delay+" HOUR) FROM `player_mission` WHERE `mission_id` = "+.@id[.@i]+" AND `completion` <> '0000-00-00 00:00:00'",.@mission_completed,.@diff_delay,.@day$ );
			if( .@repeatable[.@i] && ( ( .@mission_completed >= .@repeatable[.@i] ) || ( .@diff_delay && .@diff_delay <= .@redo_delay ) ) ){
				next;
				mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
				mes "-------------------------------";
				mes " ";
				if( .@mission_completed >= .@repeatable[.@i] ){
					mes "您无法领取该任务,您在冒险家工会的经验不足. 至少需要完成 "+.@mission_completed+" 次冒险家工会的委托任务才可以领取该任务.";
					close;
				}
				if( .@diff_delay && .@diff_delay <= .@redo_delay ){
					mes "您无法领取该任务, 您的上一个任务委托冷却时间还未到.";
					mes "还剩大约 : ^FF0000"+.@day$+"^000000";
					close;
				}
			}
		}
		
		if( .@result ){
			message strcharinfo(0),"<冒险家工会> 无法接受该委托任务.";
		}else{
			if( select( "- 接受该委托任务","- 取消" ) == 1 ){
				query_sql( "INSERT INTO `player_mission` VALUES ( "+gettimetick(2)+","+.@mission_id+","+getcharid(3)+","+getcharid(0)+",'"+escape_sql( strcharinfo(0) )+"','',"+( .@timelimit + gettimetick(2) )+",NOW(),'0000-00-00 00:00:00' );" );
				message strcharinfo(0),"<冒险家工会> 接受了1条委托任务 ID# "+.@mission_id;

				@ms_list$[ @ms_size ] = ""+.@mission_id;
				@ms_size++;
		
				setd( "@ms_"+.@mission_id+"_expire" ),( .@timelimit + gettimetick(2) );
				copyarray getd( "@ms_"+.@mission_id+"_list[0]" ),.@monster_list[0],.@monster_size;
				copyarray getd( "@ms_"+.@mission_id+"_qty[0]" ),.@monster_qty[0],.@monster_size;
				deletearray getd( "@ms_"+.@mission_id+"_hunt" );
				addtimer ( .@timelimit * 1000 ),.npc_name$+"::OnTimeCheck";
			}
		}
		break;
	case 3:
		query_sql( "SELECT `id`,`title`,`min_lv`,`max_lv` FROM `mission_board` WHERE `npc_id` LIKE '%|"+.@mission_npc_num+"|%' AND `id` IN ( SELECT `mission_id` FROM `player_mission` WHERE `cid` = "+getcharid(0)+" AND `completion` = '0000-00-00 00:00:00' )",.@id,.@title$,.@min_lv,.@max_lv );
		.@id_size = getarraysize( .@id );
		for( .@i = 0; .@i < .@id_size; .@i++ )
			.@mission_menu$ = .@mission_menu$ + "["+.@min_lv[.@i]+"~"+.@max_lv[.@i]+"] "+.@title$[.@i] +":";
		mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
		mes "-------------------------------";
		mes "请选择需要放弃的委托任务.";
		next;
		.@i = select( .@mission_menu$ ) - 1;
		mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
		mes "-------------------------------";
		mes "确定放弃 ^0055FFID # "+.@id[.@i]+"^000000 的任务?";
		mes "标题 : ^0055FF"+.@title$[.@i]+"^000000";
		mes "^777777( 此操作无法恢复 )^000000";
		if( select( "- 取消","- 确定" ) == 2 ){
			message strcharinfo(0),"<冒险家工会> 任务已删除. 您放弃了1个委托任务,任务ID# "+.@id[.@i];
			query_sql( "DELETE FROM `player_mission` WHERE `mission_id` = "+.@id[.@i]+" AND `cid` = "+getcharid(0)+" AND `completion` = '0000-00-00 00:00:00'" );
			setd( "@ms_"+.@id[.@i]+"_expire" ),0;
			deletearray getd( "@ms_"+.@id[.@i]+"_list" );
			deletearray getd( "@ms_"+.@id[.@i]+"_qty" );
			deletearray getd( "@ms_"+.@id[.@i]+"_hunt" );
			for( .@ms = 0; .@ms < @ms_size; .@ms++ )
				if( ""+.@id[.@i] == @ms_list$[.@ms] ){
					deletearray @ms_list$[.@ms],1;
					@ms_size--;
					break;
				}
		}
		break;
	case 4:
		// get info from SQL.
		do{
			mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
			mes "-------------------------------";
			deletearray .@id;
			query_sql( "SELECT `id`,`title`,`min_lv`,`max_lv` FROM `mission_board` WHERE `npc_id` LIKE '%|"+.@mission_npc_num+"|%' LIMIT "+.max_page_size+" OFFSET "+.@offset,.@id,.@title$,.@min_lv,.@max_lv );
			.@offset += .max_page_size;
			.@size = getarraysize( .@id );
			if( !.@size ){
				mes "冒险家工会委托任务列表中没有需要更新发布的任务.";
				close;
				
			}else{
				mes "请在列表中选择一个需要发布的委托任务.";
				.@mission_menu$ = "";
				for( .@i = 0; .@i < .@size; .@i++ )
					.@mission_menu$ = .@mission_menu$ + "["+.@min_lv[.@i]+"~"+.@max_lv[.@i]+"] "+.@title$[.@i] +":";
			}
			next;
			.@i = select( .@mission_menu$+ ( ( .@size < .max_page_size )?"":"- 下一页" ) ) - 1;
		}while( .@i == .@size );
		query_sql( "SELECT * FROM `mission_board` WHERE `id` = "+.@id[.@i],
			.@new_mission_id,
			.@title$,
			.@description$,
			.@mob_list$,
			.@mob_qty$,
			.@item_list$,
			.@item_qty$,
			.@base_job_bitmask,
			.@job_branch_bitmask,
			.@min_lv,
			.@max_lv,
			.@repeatable,
			.@timelimit,
			.@reward_list$,
			.@reward_qty$,
			.@baseexp,
			.@jobexp,
			.@zeny,
			.@cash,
			.@aid,
			.@name$,
			.@time_update$,
			.@npc_id$,
			.@redo_delay
		);
		// explode all saved strings to array value.
		.@monster_size = callsub( OnExplodeArray,.@mob_list$,.@monster_list,0 );
		.@monster_size = callsub( OnExplodeArray,.@mob_qty$,.@monster_qty,0 );
		
		.@item_size = callsub( OnExplodeArray,.@item_list$,.@item_list,0 );
		.@item_size = callsub( OnExplodeArray,.@item_qty$,.@item_qty,0 );
		
		.@reward_size = callsub( OnExplodeArray,.@reward_list$,.@reward_list,0 );
		.@reward_size = callsub( OnExplodeArray,.@reward_qty$,.@reward_qty,0 );
		
		.@selected_npc_size = callsub( OnExplodeArray,.@npc_id$,.@selected_npc_array$,1 );
	
		setarray .@level_range,.@min_lv,.@max_lv;

	case 5:
		do{
			mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
			mes "-------------------------------";
			// display the information of mission
			callsub( OnDisplayMissionInfo,
				.@new_mission_id,
				.@title$,
				.@description$,
				.@level_range,
				.@repeatable,
				( .@timelimit + gettimetick(2) ),
				.@monster_list,
				.@monster_qty,
				.@item_list,
				.@item_qty,
				.@base_job_bitmask,
				.@job_branch_bitmask,
				.@baseexp,
				.@jobexp,
				.@cash,
				.@zeny,
				.@reward_list,
				.@reward_qty,
				.@selected_npc_array$,
				.@time_update$,
				.@redo_delay,
				0
			);
			
			// check if required info complete for setup mission
			.@incomplete = 0;
			if( .@title$ == "" ) .@incomplete |= 1;
			if( .@description$ == "" ) .@incomplete |= 2;
			if( !.@monster_size && !.@item_size ) .@incomplete |= 4;
			if( getarraysize( .@level_range ) != 2 || !.@base_job_bitmask || !.@job_branch_bitmask ) .@incomplete |= 8;
			if( !.@reward_size && !.@baseexp && !.@jobexp && !.@cash && !.@zeny ) .@incomplete |= 16;
			if( !.@selected_npc_size ) .@incomplete |= 32;
					
			.@main_option = select( "- 设置委托任务标题 "+(( .@incomplete & 1 )?"^FF0000-未设置-^000000":"" ),
							"- 设置委托任务描述 "+(( .@incomplete & 2 )?"^FF0000-未设置-^000000":"" ),
							"- 设置怪物列表 "+(( .@incomplete & 4 )?"^FF0000-未设置-^000000":( ( !.@monster_size )?"^777777<none>^000000":"" )),
							"- 设置物品列表 "+(( .@incomplete & 4 )?"^FF0000-未设置-^000000":( ( !.@item_size )?"^777777<none>^000000":"" )),
							"- 设置职业/等级限制 "+(( .@incomplete & 8 )?"^FF0000-未设置-^000000":"" ),
							"- 设置时限/单次/日常/等限制 ",
							"- 设置奖励内容 "+(( .@incomplete & 16 )?"^FF0000-未设置-^000000":"" ),
							"- 设置发布任务的NPC "+(( .@incomplete & 32 )?"^FF0000-未设置-^000000":"" ),
							( .@incomplete )?"":"^0055FF- 完成委托任务设置^000000" );
			next;				
			switch( .@main_option ){
				case 1:
					mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
					mes "-------------------------------";
					mes "请输入委托任务的标题.";
					mes "^777777<字数: 2~15个汉字>^000000";
					while( input( .@title$,4,30 ) );
					.@input_result = replacestr( .@title$,":"," " );
					break;
				case 2:
					.@description$ = "";
					do{
						mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
						mes "-------------------------------";
						mes "请输入委托任务的描述,可多次输入. 为了排版格式请合理使用空格.";
						mes "^777777<字数: 2~127个汉字>^000000";
						mes " ";
						mes "^0055FF"+.@description$+"^000000";
						.@length = getstrlen( .@description$ );
						do{
							.@input_result = input( .@temp_input$,4,255 );
							if( .@input_result )
								message strcharinfo(0),"<冒险家工会> 任务描述的字数范围是: 2~127个汉字.";
						}while( .@input_result );
						.@description$ = .@description$ + " "+ .@temp_input$;
						mes "^0055FF"+.@temp_input$+"^000000";
						.@length = getstrlen( .@description$ );
						next;
					}while( select( ( .@length >= 255 )?"":"- 添加多行文字描述 ^777777<还可以输入 "+( 255 - .@length )+"个字>^000000","- 返回" ) == 1 );
					.@description$  = replacestr( .@description$,":"," " );
					break;
				case 3:
					if( .@new_mission_id ){
						dispbottom "编辑怪物列表可能造成不可预见的脚本错误,包含如下可能:";
						dispbottom " > 脚本或委托任务无法正常运行.";
						dispbottom " > 影响现有已接受委托的冒险者的进度.";
						dispbottom " > 其它未知可能.";
						dispbottom "请尽可能不要修改该列表,或者联系GM安赫尔来进行特殊设定.";
						dispbottom "<若必须则请重新添加委托任务>";
					}
					do{
						mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
						mes "-------------------------------";
						mes "怪物列表:";
						if( .@monster_size ){
							.@mob_menu$ = "- ";
							.@current_mob_list$ = "|";
							deletearray .@current_mob$;
							for( .@i = 0; .@i < .@monster_size; .@i++ ){
								.@mob_name$ = getmonsterinfo( .@monster_list[.@i],MOB_NAME );
								.@mob_menu$ = .@mob_menu$ + .@monster_qty[.@i] +" x "+.@mob_name$ +":";
								mes " ^777777 - "+.@monster_qty[.@i]+" x "+.@mob_name$+"^000000";
								.@current_mob_list$ = .@current_mob_list$ + .@monster_list[.@i] +"|";
							}
							
						}else{
							mes " ^777777 <none> ^000000";
						}
						mes " ";
						.@option = select( ( .@monster_size >= .max_required_monster )?"":"- 添加怪物",( .@monster_size )?"- 删除怪物":"","- 返回" );
						switch( .@option ){
							case 1:
								mes "请输入需要击杀怪物的ID";
								do{
									input .@mob_id;
									if( !.@mob_id ) break;
									.@mob_name$ = getmonsterinfo( .@mob_id,MOB_NAME );
								}while( .@mob_name$ == "null" );
								if( .@mob_name$ != "null" && .@mob_id ){
									mes "请设置需要击杀 "+.@mob_name$+" 的数量?";
									input .@amount,0,30000;
									if( .@amount ){
										if( compare( "|"+.@current_mob_list$+"|","|"+.@mob_id+"|" ) ){
											for( .@i = 0; .@i < .@monster_size; .@i++ )
												if( .@monster_list[.@i] == .@mob_id ){
													.@monster_qty[.@i] += .@amount;
													break;
												}
												
										}else{
											.@monster_list[.@monster_size] = .@mob_id;
											.@monster_qty[.@monster_size] = .@amount;
											.@monster_size++;
										}
									}
								}
								break;
							case 2:
								mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
								mes "-------------------------------";
								mes "请选择需要删除的怪物.";
								.@i = select( .@mob_menu$+"- 返回" ) - 1;
								if( .@i < .@monster_size ){
									deletearray .@monster_list[.@i],1;
									deletearray .@monster_qty[.@i],1;
									.@monster_size--;
								}
							default: break;
						}
						if( .@option < 3 ) next;
					}while( .@option < 3 );
					break;
				case 4:
					do{
						mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
						mes "-------------------------------";
						mes "委托任务的收集物品需求: ";
						if( .@item_size ){
							.@item_menu$ = "- ";
							.@current_item_list$ = "|";
							deletearray .@current_item$;
							for( .@i = 0; .@i < .@item_size; .@i++ ){
								.@item_name$ = getitemname( .@item_list[.@i] );
								.@item_menu$ = .@item_menu$ + .@item_qty[.@i] +" x "+.@item_name$ +":";
								mes " ^777777 - "+.@item_qty[.@i]+" x "+.@item_name$+"^000000";
								.@current_item_list$ = .@current_item_list$ + .@item_list[.@i] + "|";
							}
							
						}else{
							mes " ^777777 <none> ^000000";
						}
						mes " ";
						.@option = select( ( .@item_size >= .max_required_item )?"":"- 添加物品",( .@item_size )?"- 删除物品":"","- 返回" );
						switch( .@option ){
							case 1:
								do{
									input .@item_id;
									if( !.@item_id ) break;
									.@item_name$ = getitemname( .@item_id );
								}while( .@item_name$ == "null" || .@item_name$ == "" );
								if( .@item_id && .@item_name$ != "null" && .@item_name$ != "" ){
									mes "请设置需要收集的 "+.@item_name$+" 数量?";
									input .@amount,0,30000;
									if( .@amount ){
										if( compare( "|"+.@current_item_list$+"|","|"+.@item_id+"|" ) ){
											for( .@i = 0; .@i < .@item_size; .@i++ )
												if( .@item_list[.@i] == .@item_id ){
													.@item_qty[.@i] += .@amount;
													break;
												}
										}else{
											.@item_list[.@item_size] = .@item_id;
											.@item_qty[.@item_size] = .@amount;
											.@item_size++;
										}
									}
								}
								break;
							case 2:
								mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
								mes "-------------------------------";
								mes "请选择需要删除的物品.";
								.@i = select( .@item_menu$+" - 返回" ) - 1;
								if( .@i < .@item_size ){
									mes "删除 "+.@item_qty[.@i]+" x "+getitemname( .@item_list[.@i] );
									deletearray .@item_list[.@i],1;
									deletearray .@item_qty[.@i],1;
									.@item_size--;
								}
							default: break;
						}
						if( .@option < 3 ) next;
					}while( .@option < 3 );
					break;
				case 5: // class limitation
					if( !.@base_job_bitmask ){
						// enable all job by default.
						for( .@i = 0; .@i < .base_job_size; .@i++ ){
							.@bitmask_value = ( 1 << .@i );
							.@base_job_bitmask |= .@bitmask_value;
						}
						// enable all inherited classes by default.
						for( .@i = 0; .@i < .job_branch_size; .@i++ ){
							.@bitmask_value = ( 1 << .@i );
							.@job_branch_bitmask |= .@bitmask_value;
						}
					}
					
					do{
						mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
						mes "-------------------------------";
						mes "等级范围 : "+( ( .@level_range[1] )? "^777777"+.@level_range[0]+"~"+.@level_range[1]:"^FF0000-未设置-" )+"^000000";
						mes "可领取委托的职业限制";
						if( .@base_job_bitmask ){
							for( .@i = 0; .@i < .base_job_size; .@i++ )
								if( .@base_job_bitmask & ( 1 << .@i ) )
									mes " ^777777 - "+jobname( roclass( .base_job[.@i] ) )+" ^000000";
						}else{
							mes " ^FF0000 -未设置-^000000";
						}
						mes " ";
						mes "继承上一级的设置";
						if( .@job_branch_bitmask ){
							for( .@i = 0; .@i < .job_branch_size; .@i++ )
								if( .@job_branch_bitmask & ( 1 << .@i ) )
									mes " ^777777 - "+.job_branch_name$[.@i]+" ^000000";	
						}else{
							mes " ^FF0000 -未设置-^000000";
						}	
						next;
						.@option = select( "- 设置基础职业","- 设置职业限制","- 设置等级范围","- 返回" );
						switch( .@option ){
							case 1:
								dispbottom "[ 基本职业 ] 红色 = 禁用 , 绿色 = 启用";
								do{
									mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
									mes "-------------------------------";
									mes "激活职业列表:";
									.@base_job_menu$ = "";
									for( .@i = 0; .@i < .base_job_size; .@i++ ){
										.@job_name$ = jobname( roclass( .base_job[.@i] ) );
										if( .@base_job_bitmask & ( 1 << .@i ) )
											mes " ^777777 - "+.@job_name$+" ^000000";
										.@base_job_menu$ = .@base_job_menu$ + (( .@base_job_bitmask & ( 1 << .@i ) )?"^4EEE94":"^FF0000" ) + .@job_name$ +"^000000:";
									}
									next;
									.@i = select( .@base_job_menu$+"- 返回" ) - 1;
									if( .@i < .base_job_size ){
										.@bitmask_value = ( 1 << .@i );
										if( .@base_job_bitmask & .@bitmask_value )
											.@base_job_bitmask -= .@bitmask_value;
										else
											.@base_job_bitmask |= .@bitmask_value;
									}
								}while( .@i < .base_job_size );
								break;
							case 2:
								dispbottom "[ 职业限制 ] 红色 = 禁用 , 绿色 = 启用";
								do{
									mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
									mes "-------------------------------";
									mes "继承上一级职业限定设置:";
									.@job_branch_menu$ = "";
									for( .@i = 0; .@i < .job_branch_size; .@i++ ){
										if( .@job_branch_bitmask & ( 1 << .@i ) )
											mes " ^777777 - "+.job_branch_name$[.@i]+" ^000000";
										.@job_branch_menu$ = .@job_branch_menu$ + (( .@job_branch_bitmask & ( 1 << .@i ) )?"^4EEE94":"^FF0000" ) + .job_branch_name$[.@i] +"^000000:";
									}
									next;
									.@i = select( .@job_branch_menu$+"- 返回" ) - 1;
									if( .@i < .job_branch_size ){
										.@bitmask_value = ( 1 << .@i );
										if( .@job_branch_bitmask & .@bitmask_value )
											.@job_branch_bitmask -= .@bitmask_value;
										else
											.@job_branch_bitmask |= .@bitmask_value;
									}
								}while( .@i < .job_branch_size );
								break;
							case 3:
								mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
								mes "-------------------------------";
								mes "最低可领取该委托任务的级别";
								input .@level_range[0],1,.server_max_level ;
								mes "最高可领取该委托任务的级别";
								input .@level_range[1],.@level_range[0],.server_max_level;
							default: break;
						}
						if( .@option < 4 ) next;
					}while( .@option < 4 );
					break;
				case 6: // mission limitation
					do{
						mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
						mes "-------------------------------";
						mes "可重复的 : ^777777"+( ( !.@repeatable )?"无限制":""+.@repeatable )+" 次^000000";
						mes "冷却时间 : ^777777"+( ( !.@redo_delay )?"无":callsub( OnTime2Str,( ( .@redo_delay * 3600 ) + gettimetick(2) ) ))+"^000000";
						mes "时间限制 : ^777777"+( ( !.@timelimit )?"无":callsub( OnTime2Str,( .@timelimit + gettimetick(2) ) ) )+"^000000";
						.@option = select( "- 设置单次日常","- 设置时间限制","- 设置冷却时间","- 设置前置任务","- 取消" );
						switch( .@option ){
							case 1:
								mes "请设置该委托任务可重复完成的次数:";
								mes "^777777( 0 = 无限制 )^000000";
								input .@repeatable,0,100;
								break;
							case 2:
								mes "请设置该委托任务完成时间限定:";
								mes "       1 = 1分钟";
								mes "    60 = 1小时";
								mes "1440 = 1天";
								input .@timelimit,0,50000;
								.@timelimit *= 60;
								break;
							case 3:
								mes "请设置该委托任务的冷却时间:";
								mes "     1 =  1小时";
								mes "   24 =  1天";
								mes " 720 = 30天";
								input .@redo_delay,0,50000;
								break;
							case 4:
								mes "前置任务设置功能尚在开发中,因此在定义内容设置时会有较多限制!";
								break;
								
								do{
									mes "前置任务要求:";
									if( .@required_mission_size ){
										for( .@i = 0; .@i < .@required_mission_size; .@i++ ){
											mes " ^777777 - "+.@required_mission$[.@i]+"^000000";
											.@required_mission_menu$ = .@required_mission_menu$ + .@required_mission$[.@i] +":";
										}
										
									}else{
										mes "^777777 <none> ^000000";
									}
									next;
									.@sub_option = select( ( .@required_mission_size < .max_required_mission )?"- 添加前置需求任务":"",
														( .@required_mission_size )?"- 删除前置需求任务":"",
														"- 返回");
									switch( .@sub_option ){
										case 1:
											mes "请设置作为前置任务的任务ID";
											mes "^777777<输入'0'取消>^000000";
											do{
												input .@mission_id$;
												if( .@mission_id$ == "0" ) break;
											}while( compare( "|"+.@required_mission_menu$+"|","|"+.@mission_id$+"|" ) );
											.@mission_id$ = replacestr( .@mission_id$,":","" );
											if( .@mission_id$ != "0" ){
												.@required_mission$[.@required_mission_size] = .@mission_id$;
												.@required_mission_size++;
											}
											break;
										case 2:
											mes "请选择需要删除的前置任务.";
											.@i = select( .@required_mission_menu$ ) - 1;
											deletearray .@required_mission$[.@i],1;
										default: break;
									}
								}while( .@sub_option < 3 );
							default: break;
						}
						if( .@option < 5 ) next;
					}while( .@option < 5 );
					break;
				case 7: // reward list
					do{
						mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
						mes "-------------------------------";
						mes " ";
						mes "现金点数 : ^777777"+( ( .@cash )? .@cash:"<none>" )+"^000000";
						mes "Zeny : ^777777"+( ( .@zeny )? .@zeny:"<none>" )+"^000000";
						mes "物品奖励列表 : ";
						if( .@reward_size ){
							.@reward_menu$ = "";
							deletearray .@current_reward$;
							for( .@i = 0; .@i < .@reward_size; .@i++ ){
								.@reward_name$ = getitemname( .@reward_list[.@i] );
								.@reward_menu$ = .@item_menu$ + .@reward_qty[.@i] +" x "+.@reward_name$ +":";
								.@current_reward$[.@i] = .@reward_name$;
								mes " ^777777 - "+.@reward_qty[.@i]+" x "+.@reward_name$+"^000000";
							}
							.@current_reward_list$ = implode( .@current_reward$,"|" );
							
						}else{
							mes " ^777777 <none> ^000000";
						}
						mes "基本经验 : ^777777"+( ( .@baseexp )? .@baseexp:"<none>" )+"^000000";
						mes "职业经验 : ^777777"+( ( .@jobexp )? .@jobexp:"<none>" )+"^000000";
						mes " ";
						next;
						.@option = select( ( .@reward_size >= .max_required_item )?"":"- 添加奖励物品",
										( .@reward_size )?"- 删除奖励物品":"",
										"- 设置现金点数奖励",
										"- 设置Zeny奖励",
										"- 设置基本经验奖励",
										"- 设置职业经验奖励",
										"- 返回" );
						mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
						mes "-------------------------------";
						switch( .@option ){
							case 1:
								mes "请输入奖励物品ID";
								do{
									input .@reward_id;
									if( !.@reward_id ) break;
									.@reward_name$ = getitemname( .@reward_id );
								}while( .@reward_name$ == "<null>" || .@reward_name$ == "" );
								if( .@reward_id && .@reward_name$ != "null" && .@reward_name$ != "" ){
									mes "请问需要奖励多少个 "+.@reward_name$+" ?";
									input .@amount,0,30000;
									if( .@amount ){
										if( compare( "|"+.@current_reward_list$+"|","|"+.@reward_name$+"|" ) ){
											for( .@i = 0; .@i < .@reward_size; .@i++ )
												if( .@reward_list[.@i] == .@item_id ){
													.@reward_qty[.@i] += .@amount;
													break;
												}
										}else{
											.@reward_list[.@reward_size] = .@reward_id;
											.@reward_qty[.@reward_size] = .@amount;
											.@reward_size++;
										}
									}
								}
								break;
							case 2:
								mes "请选择需要删除的奖励内容.";
								.@i = select( .@reward_menu$ ) - 1;
								mes "删除 "+.@reward_qty[.@i]+" x "+getitemname( .@reward_list[.@i] );
								deletearray .@reward_list[.@i],1;
								deletearray .@reward_qty[.@i],1;
								.@reward_size--;
								break;
							case 3:
								mes "请问需要奖励多少现金点数(P点)?";
								mes "^777777<数值范围: 0~"+.max_integer_value+">^000000";
								input .@cash,0,.max_integer_value;
								break;
							case 4:
								mes "请问需要奖励多少Zeny?";
								mes "^777777<数值范围: 0~"+.max_integer_value+">^000000";
								input .@zeny,0,.max_integer_value;
								break;
							case 5:
								mes "请问需要奖励多少基本经验?";
								mes "^777777<数值范围: 0~"+.max_integer_value+">^000000";
								input .@baseexp,0,.max_integer_value;
								break;
							case 6:
								mes "请问需要奖励多少职业经验?";
								mes "^777777<数值范围: 0~"+.max_integer_value+">^000000";
								input .@jobexp,0,.max_integer_value;
							default: break;
						}
						next;
					}while( .@option < 7 );
					break;
				case 8: // npc limitation
					mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
					mes "-------------------------------";
					mes "默认设置情况下,委托的任务将会出现在所有的冒险家工会委托任务面板或NPC的委托列表中 ^FF0000除非您对冒险家工会委托任务面板或NPC进行过特殊设定.^000000";
					mes "如果没有特别的情况,只需要选择全部NPC即可.";
					next;
					if( !getarraysize( .@selected_npc_array$ ) )
						for( .@i = 0; .@i < .mission_npc_count; .@i++ ){
							.@selected_npc_array$[.@i] = ""+.@i;
							.@selected_npc_size++;
						}
					dispbottom "[ 冒险家工会委托任务面板与NPC限制 ] 红色 = 禁用 , 绿色 = 启用";
			
					do{
						mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
						mes "-------------------------------";
						mes "选择任务出现的面板或NPC: ";
						.@selected_npc_menu$ = "";
						if( .@selected_npc_size >= 2 ){
							.@selected_npc$ = "|"+implode( .@selected_npc_array$,"|" )+"|";
						}else{
							.@selected_npc$ = "|"+.@selected_npc_array$+"|";
						}
						for( .@i = 0; .@i < .mission_npc_count; .@i++ ){
							getmapxy( .@map$,.@x,.@y,1,.npc_unique_list$[.@i] );
							.@sub_npc_name$ = ( ( compare( "|"+.@selected_npc$+"|","|"+.@i+"|" ) )?"^44EE00":"^FF0000" );
							.@sub_npc_name$ = .@sub_npc_name$ + .npc_name_list$[.@i];
							mes "^777777("+.@map$+") "+.@sub_npc_name$;
							.@selected_npc_menu$ = .@selected_npc_menu$ + .@sub_npc_name$ +":";
						}
						next;
						.@option = select( .@selected_npc_menu$+"^000000- 返回" ) - 1;
						if( .@option < .mission_npc_count ){
							if( compare( "|"+.@selected_npc$+"|","|"+.@option+"|" ) )
								.@selected_npc_array$[.@option] = "";
							else
								.@selected_npc_array$[.@option] = ""+.@option;
							.@selected_npc_size = getarraysize( .@selected_npc_array$ );
						}
						// dispbottom "["+rand(10,99)+"] "+implode( .@selected_npc_array$,"|" );
					}while( .@option < .mission_npc_count );
					break;
				default: break;
			}
			next;
		}while( .@main_option < 9 );
		
		// finalise all variable
		if( .@monster_size ){
			.@final_mob_list$ = "|";
			.@final_mob_qty$ = "|";
			for( .@i = 0; .@i < .@monster_size; .@i++ ){
				.@final_mob_list$ = .@final_mob_list$ + .@monster_list[.@i] +"|";
				.@final_mob_qty$ = .@final_mob_qty$ + .@monster_qty[.@i] +"|";
			}	
		}
		
		if( .@item_size ){
			.@final_item_list$ = "|";
			.@final_item_qty$ = "|";
			for( .@i = 0; .@i < .@item_size; .@i++ ){
				.@final_item_list$ = .@final_item_list$ + .@item_list[.@i] +"|";
				.@final_item_qty$ = .@final_item_qty$ + .@item_qty[.@i] +"|";
			}
		}
		
		if( .@reward_size ){
			.@final_reward_list$ = "|";
			.@final_reward_qty$ = "|";
			for( .@i = 0; .@i < .@reward_size; .@i++ ){
				.@final_reward_list$ = .@final_reward_list$ + .@reward_list[.@i] +"|";
				.@final_reward_qty$ = .@final_reward_qty$ + .@reward_qty[.@i] +"|";
			}
		}
		
		if( .@selected_npc_size ){
			.@final_npc_list$ = "|";
			for( .@i = 0; .@i < .@selected_npc_size; .@i++ )
				.@final_npc_list$ = .@final_npc_list$ + .@selected_npc_array$[.@i] +"|";
		}
		
		if( !.@new_mission_id ){
			.@new_mission_id = gettimetick(2);
			message strcharinfo(0),"<冒险家工会> 委托任务 ID# "+.@new_mission_id+" 已经成功添加.";
		}else{
			message strcharinfo(0),"<冒险家工会> 委托任务 ID# "+.@new_mission_id+" 已经发布.";
			// attach and inform other online players.
			callsub( OnRemoveMission,.@new_mission_id,"冒险家工会发布了新任务 ID# "+.@new_mission_id );
		}
		
		// add new mission into SQL
		query_sql( 
			"REPLACE INTO `mission_board` VALUES ( " +
				.@new_mission_id+", " +
				"'"+escape_sql( .@title$ )+"', " +
				"'"+escape_sql( .@description$ )+"', " +
				"'"+escape_sql( .@final_mob_list$ )+"', " +
				"'"+escape_sql( .@final_mob_qty$ )+"', " +
				"'"+escape_sql( .@final_item_list$ )+"', " +
				"'"+escape_sql( .@final_item_qty$ )+"', " +
				.@base_job_bitmask+", " +
				.@job_branch_bitmask+", " +
				.@level_range[0]+", " +
				.@level_range[1]+", " +
				.@repeatable+", " +
				.@timelimit+", " +
				"'"+escape_sql( .@final_reward_list$ )+"', " +
				"'"+escape_sql( .@final_reward_qty$ )+"', " +
				.@baseexp+", " +
				.@jobexp+", " +
				.@zeny+", " +
				.@cash+", " +
				getcharid(3)+", " +
				"'"+escape_sql( strcharinfo(0) )+"', " +
				"NOW(), " +
				"'"+escape_sql( .@final_npc_list$ )+"', " +
				.@redo_delay +
			" ); " 
		);
		break;
	case 6: // delete mission
		do{
			mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
			mes "-------------------------------";
			deletearray .@id;
			query_sql( "SELECT `id`,`title`,`min_lv`,`max_lv` FROM `mission_board` WHERE `npc_id` LIKE '%|"+.@mission_npc_num+"|%' LIMIT "+.max_page_size+" OFFSET "+.@offset,.@id,.@title$,.@min_lv,.@max_lv );
			.@offset += .max_page_size;
			.@size = getarraysize( .@id );
			if( !.@size ){
				mes "没有可发布的委托任务.";
				close;
				
			}else{
				mes "选择一个委托任务.";
				for( .@i = 0; .@i < .@size; .@i++ )
					.@mission_menu$ = .@mission_menu$ + "["+.@min_lv[.@i]+"~"+.@max_lv[.@i]+"] "+.@title$[.@i] +":";
			}
			next;
			.@i = select( .@mission_menu$+ ( ( .@size < .max_page_size )?"":"- 下一页" ) ) - 1;
		}while( .@i == .@size );
		.@mission_id = .@id[.@i];
		mes "- ^cc6600冒险家工会^000000 - ^cccccc"+.@npc_name$+"^000000";
		mes "-------------------------------";
		mes "您确定要删除这个已发布委托任务吗?";
		if( select( "- 取消","- 删除任务" ) == 2 ){
			query_sql( "DELETE FROM `mission_board` WHERE `id` = "+.@mission_id+" LIMIT 1" );
			mes "委托任务已经从发布任务列表中删除.";

			// attach other online players and remove the missions.
			callsub( OnRemoveMission,.@mission_id,"冒险家工会取消了任务 # "+.@mission_id );
			query_sql( "DELETE FROM `player_mission` WHERE `completion` = '0000-00-00 00:00:00' AND `mission_id` = "+.@mission_id );
			mes "已经将任务从所有玩家身上删除.";
		}
	default: break;
}
close;

OnInit:
initnpctimer "mission_board";
// initialize settings
if( strnpcinfo(0) == "mission_board" ){
	
	// gm level to access panel
	.gm_level = 0;
	// max no. of required monster
	.max_required_monster = 6000;
	// max no. of required item
	.max_required_item = 35000;
	// max no. of required mission ( un-implement yet )
	.max_required_mission = 10;
	// max no. of available mission
	.max_mission_available = 50;
	// max value of integer input
	.max_integer_value = 10000000;
	// max amount of mission per page
	.max_page_size = 6;
	// max mission per npc take by character
	.max_mission_per_char = 10;
	
	// predefined values.
	.npc_name$ = strnpcinfo(0);
	.server_max_level = getbattleflag( "max_lv" );
	setarray .base_job,
		EAJ_SWORDMAN,
		EAJ_MAGE,
		EAJ_ARCHER,
		EAJ_ACOLYTE,
		EAJ_MERCHANT,
		EAJ_THIEF,
		EAJ_TAEKWON,
		EAJ_GUNSLINGER,
		EAJ_NINJA;
	.base_job_size = getarraysize( .base_job );
	setarray .job_branch_name$,
		"2-1 职业",
		"2-2 职业",
		"转生职业",
		"宝宝职业",
		"三转职业";
	setarray .job_branch,
		EAJL_2_1,
		EAJL_2_2,
		EAJL_UPPER,
		EAJL_BABY,
		EAJL_THIRD;
	.job_branch_size = getarraysize( .job_branch );
	
}else{
	// delay the process
	.@num = atoi( strnpcinfo(2) );
	if( .@num && .@num <= 500 && .mission_npc_count < 100 ){
		sleep( .@num + 1 );
		.npc_name_list$[ .mission_npc_count ] = strnpcinfo(1);
		.npc_unique_list$[ .mission_npc_count ] = strnpcinfo(0);
		// debugmes "["+.mission_npc_count+"]"+.npc_name_list$[ .mission_npc_count ]+"|"+.npc_unique_list$[ .mission_npc_count ];
		.mission_npc_count++;
		end;
		
	}else if( !.@num ){
		debugmes "[删除] "+strnpcinfo(0)+", 错误 <代码:'"+strnpcinfo(2)+"'>";
	}else if( .mission_npc_count >= 100 ){
		debugmes "[忽略] "+strnpcinfo(0)+", 最大: NPC数量为100个.";
	}
	disablenpc strnpcinfo(0);
}
end;

// just used to display how many total Mission Board NPC.
OnTimer1000:
	stopnpctimer;
	debugmes "[ 冒险家工会 委托任务面板NPC ] 数量 : "+.mission_npc_count+" 个NPC已读取...";
	end;

// OnWhisperGlobal:
OnPCLoginEvent:
if( strnpcinfo(0) == .npc_name$ ){
	.@timetick = gettimetick(2);
	query_sql( "SELECT `mission_id`,`expire`,`mob_hunt` FROM `player_mission` WHERE `completion` = '0000-00-00 00:00:00' AND `cid` = "+getcharid(0),@ms_list$,.@expire,.@mob_hunt$ );
	@ms_size = getarraysize( @ms_list$ );
	if( @ms_size )
		for( .@i = 0; .@i < @ms_size; .@i++ ){
			if( .@timetick < .@expire[.@i] ){
				.@timeleft = ( .@expire[.@i] - .@timetick );
				addtimer ( .@timeleft * 1000 ),.npc_name$+"::OnTimeCheck";
				dispbottom "[ 冒险家工会 委托任务进度 : "+@ms_list$[.@i]+" , 将于 "+callsub( OnTime2Str,( .@timeleft + .@timetick ) )+" 后过期 ]";
				query_sql( "SELECT `mob_list`,`mob_qty` FROM `mission_board` WHERE `id` = "+@ms_list$[.@i],.@mob_list$,.@mob_qty$ );
				
				setd( "@ms_"+@ms_list$[.@i]+"_expire" ),.@expire[.@i];
				if( callsub( OnExplodeArray,.@mob_list$[.@i],getd( "@ms_"+@ms_list$[.@i]+"_list" ),0 ) ){
					callsub( OnExplodeArray,.@mob_hunt$[.@i],getd( "@ms_"+@ms_list$[.@i]+"_qty" ),0 );
					.@monster_size = callsub( OnExplodeArray,.@mob_hunt$[.@i],getd( "@ms_"+@ms_list$[.@i]+"_hunt" ),0 );
					// for( .@mob = 0; .@mob < .@monster_size; .@mob++ )
						// dispbottom  " - 击杀: "+getd( "@ms_"+@ms_list$[.@i]+"_hunt["+.@mob+"]" )+"/"+getd( "@ms_"+@ms_list$[.@i]+"_qty["+.@mob+"]" )+" x "+getmonsterinfo( getd( "@ms_"+@ms_list$[.@i]+"_list["+.@mob+"]" ),MOB_NAME );
				}
			}
		}
}
end;
	
OnPCLogoutEvent:
if( strnpcinfo(0) == .npc_name$ ){
	if( @ms_size )
		for( .@ms = 0; .@ms < @ms_size; .@ms++ ){
			debugmes "保存 "+@ms_list$[.@ms];
			.@mob_size = getarraysize( getd( "@ms_"+@ms_list$[.@ms]+"_list" ) );
			if( .@mob_size ){
				copyarray .@temp_array[0],getd( "@ms_"+@ms_list$[.@ms]+"_list[0]" ),.@mob_size;
				.@mob_hunt$ = "|";
				for( .@mob = 0; .@mob < .@mob_size; .@mob++ )
					.@mob_hunt$ = .@mob_hunt$ + getd( "@ms_"+@ms_list$[.@ms]+"_hunt["+.@mob+"]" ) +"|";
				query_sql( "UPDATE `player_mission` SET `mob_hunt` = '"+escape_sql( .@mob_hunt$ )+"' WHERE `mission_id` = "+@ms_list$[.@ms]+" AND `completion` = '0000-00-00 00:00:00' AND `cid` = "+getcharid(0) );
			}
		}
}
end;

OnNPCKillEvent:
if( strnpcinfo(0) == .npc_name$ ){
	if( @ms_size ){
		.@map$ = strcharinfo(3);
		
		// by default mission wont work in PVP,GVG,Instance,Event maps
		if( compare( .@map$,"@" ) || getmapflag( .@map$,mf_gvg ) || getmapflag( .@map$,mf_pvp )){
			dispbottom "<冒险家工会> 在PvP/GvG地图, 副本地图, 活动事件地图, 将不会记录委托任务中要求的怪物击杀数量.";
			end;
		}
		
		.@timetick = gettimetick(2);
		for( .@ms = ( @ms_size - 1 ); .@ms >= 0; .@ms-- ){
			if( getd( "@ms_"+@ms_list$[.@ms]+"_expire" ) < .@timetick ){
				setd( "@ms_"+@ms_list$[.@ms]+"_expire" ),0;
				deletearray getd( "@ms_"+@ms_list$[.@ms]+"_list" );
				deletearray getd( "@ms_"+@ms_list$[.@ms]+"_qty" );
				deletearray getd( "@ms_"+@ms_list$[.@ms]+"_hunt" );
				dispbottom "<冒险家工会> ID# "+@ms_list$[.@ms]+" 的委托任务由于被删除,现已无效. ";
				deletearray @ms_list$[.@ms],1;
				@ms_size--;
				
			}else{
				.@mob_size = getarraysize( getd( "@ms_"+@ms_list$[.@ms]+"_list" ) );
				if( .@mob_size ){
					copyarray .@temp_array[0],getd( "@ms_"+@ms_list$[.@ms]+"_list[0]" ),.@mob_size;
					for( .@mob = 0; .@mob_size; .@mob++ )
						if( .@temp_array[.@mob] == killedrid ){
							.@value = getd( "@ms_"+@ms_list$[.@ms]+"_hunt["+.@mob+"]" ) + 1;
							setd( "@ms_"+@ms_list$[.@ms]+"_hunt["+.@mob+"]" ),.@value;
							dispbottom "<冒险家工会> ID# "+@ms_list$[.@ms]+" 委托任务进度: 猎杀 "+.@value+" x "+getmonsterinfo( .@temp_array[.@mob],MOB_NAME );
							break;
						}
					deletearray .@temp_array;
				}
			}
		}
	}
}
end;

OnTimeCheck:
	if( @ms_size ){
		.@timetick = gettimetick(2);
		for( .@i = ( @ms_size - 1 ); .@i >= 0; .@i-- ){
			if( getd( "@ms_"+@ms_list$[.@i]+"_expire" ) <= .@timetick ){
				setd( "@ms_"+@ms_list$[.@i]+"_expire" ),0;
				deletearray getd( "@ms_"+@ms_list$[.@i]+"_list" );
				deletearray getd( "@ms_"+@ms_list$[.@i]+"_qty" );
				deletearray getd( "@ms_"+@ms_list$[.@i]+"_hunt" );
				dispbottom "<冒险家工会> ID# "+@ms_list$[.@i]+"] 的委托任务由于被删除,现已无效. ";
				deletearray @ms_list$[.@i],1;
				@ms_size--;
			}
		}
	}
	end;
	
OnRemoveMission:
	.@mission_id = getarg(0);
	.@message$ = getarg(1);
	
	query_sql( "SELECT `aid`,`cid` FROM `player_mission` WHERE `cid` IN ( SELECT `char_id` FROM `char` WHERE `online` = 1 ) AND `mission_id` = "+.@mission_id+" AND `completion` = '0000-00-00 00:00:00' LIMIT 128",.@aid,.@cid );
	.@aid_size = getarraysize( .@aid );
	.@i = 0;
	.@origin_aid = getcharid(3);
	while( .@i < .@aid_size ){
		if( isloggedin( .@aid[.@i],.@cid[.@i] ) ){
			attachrid( .@aid[.@i] );
			for( .@ms = @ms_size; .@ms >= 0; .@ms-- )
				if( @ms_list$[.@ms] == ""+.@mission_id ){
					message strcharinfo(0),.@message$;
					dispbottom "<冒险家工会> 请访问冒险家工会委托任务NPC以获得最新委托信息.";
					setd( "@ms_"+@ms_list$[.@ms]+"_expire" ),0;
					deletearray getd( "@ms_"+@ms_list$[.@ms]+"_list" );
					deletearray getd( "@ms_"+@ms_list$[.@ms]+"_qty" );
					deletearray getd( "@ms_"+@ms_list$[.@ms]+"_hunt" );
					deletearray @ms_list$[.@ms],1;
					@ms_size--;
					break;
				}
			detachrid;
			.@count++;
		}
		.@i++;
	}
	attachrid( .@origin_aid );
	dispbottom "<冒险家工会> 该操作共计会影响 "+.@count+" 名在线玩家.";
	return;

// usage :
// .@timeleft = callsub( OnTime2Str,<time> );
OnTime2Str:
	.@time = getarg(0);
	.@left = ( .@time - gettimetick(2) );
	.@hour = ( .@left / 3600 );
	.@min = ( .@left % 3600 / 60 );
	.@sec = ( .@left % 60 );
	if( !.@left ) 
		return "<none>";
	else
		return "^777777"+( ( .@hour )? .@hour+" h ":"" ) + ( ( .@hour || .@min )? .@min+" m ":"" ) +.@sec+" s^000000";
  
  
// usage : 
// .@new_array_size = callsub( OnExplodeArray,<old string variable>,<new array variable>,<save '0'> );
OnExplodeArray:
	.@type = getarg(2);
	explode( getarg(0),getarg(0),"|" );
	.@size = getarraysize( getarg(0) );
	while( .@i < .@size ){
		if( !.@type ){
			.@value = atoi( getelementofarray( getarg(0),.@i ) );
			if( .@value ){
				set getelementofarray( getarg(1),.@new_size ),.@value;
				.@new_size++;
			}
		}else{
			set getelementofarray( getarg(1),.@new_size ),getelementofarray( getarg(0),.@i );
			.@new_size++;
		}
		.@i++;
	}
	
	return .@new_size;
	
	
// display mission information + optional progress checking
OnDisplayMissionInfo:
		.@mission_id = getarg(0);
		.@title$ = getarg(1);
		.@description$ = getarg(2);
		.@repeatable = getarg(4);
		.@timelimit = getarg(5);
		.@monster_size = getarraysize( getarg(6) );
		.@item_size = getarraysize( getarg(8) );
		.@base_job_bitmask = getarg(10);
		.@job_branch_bitmask = getarg(11);
		.@baseexp = getarg(12);
		.@jobexp = getarg(13);
		.@cash = getarg(14);
		.@zeny = getarg(15);
		.@reward_size = getarraysize( getarg(16) );
		.@selected_npc_size = getarraysize( getarg(18) );
		.@time_update$ = getarg(19);
		.@redo_delay = getarg(20);
		.@check_progress = getarg(21);
	
		if( .@check_progress & 8 )
			.@eac = eaclass();
	
		// display mission info
		mes ""+(( .@mission_id )? "^FF0000任务ID # "+.@mission_id:" " )+"^000000";
		mes "标题 : "+( ( .@title$ != "" )?"^777777"+.@title$:"^FF0000-未设置-" )+"^000000";
		mes "描述 : "+( ( .@description$ != "" )?"^777777"+.@description$:"^FF0000-未设置-" )+"^000000";
		mes " ";
		mes "等级限制 : "+( ( getelementofarray( getarg(3),0 ) )? "^777777"+getelementofarray( getarg(3),0 )+" ~ "+getelementofarray( getarg(3),1 ):"^FF0000-未设置-" )+"^000000";
		if( .@check_progress & 1 )
			if( BaseLevel >= getelementofarray( getarg(3),0 ) && BaseLevel <= getelementofarray( getarg(3),1 ) )
				.@lv_progress = 1;
		
		mes "重复完成 : ^777777"+( ( !.@repeatable )?"<默认|无限制>":.@repeatable+" 次" )+"^000000";
		mes "冷却时间 : ^777777"+( ( !.@redo_delay )?"<none>":callsub( OnTime2Str,( ( .@redo_delay * 3600 ) + gettimetick(2) ) ) )+"^000000";
		mes "时间限定 : ^777777"+( ( !.@timelimit )?"<none>":callsub( OnTime2Str,.@timelimit ) )+"^000000";
		mes " ";
		
		.@word$ = (( !.@monster_size && !.@item_size )?"^FF0000-未设置-":"^777777<none>" );
		mes "怪物列表 : "+( ( .@monster_size )?"":.@word$ )+"^000000";
		if( .@monster_size )
			for( .@i = 0; .@i < .@monster_size; .@i++ ){
				.@mob_id = getelementofarray( getarg(6),.@i );
				.@quantity = getelementofarray( getarg(7),.@i );
				if( .@check_progress & 2 ){
					.@killed_count = getd( "@ms_"+.@mission_id+"_hunt["+.@i+"]" );
					if( .@killed_count >= .@quantity ){
						.@monster_progress++;
						.@temp_color$ = "44EE99";
					}else{
						.@temp_color$ = "FF0000";
					}
				}
				mes " ^777777"+.@quantity+" x "+getmonsterinfo( .@mob_id,MOB_NAME )+"  "+( ( .@check_progress & 2 )?"^"+.@temp_color$+"(已经击杀 "+.@killed_count+" )":"" )+"^000000";
			}
		mes "物品列表 : "+( ( .@item_size )?"":.@word$ )+"^000000";
		if( .@item_size )
			for( .@i = 0; .@i < .@item_size; .@i++ ){
				.@item_id = getelementofarray( getarg(8),.@i );
				.@quantity = getelementofarray( getarg(9),.@i );
				.@item_type = getiteminfo( .@item_id,2 );
				if( .@check_progress & 4 ){
					.@item_count = countitem( .@item_id );
					if( .@item_count >= .@quantity ){
						.@item_progress++;
						.@temp_color$ = "44EE99";
					}else{
						.@temp_color$ = "FF0000";
					}
				}
				mes " ^777777"+.@quantity+" x "+getitemname( .@item_id )+" "+( ( .@item_type == 4 || .@item_type == 5 )?"["+getitemslots( .@item_id )+"]":"" )+"  "+( ( .@check_progress & 4 )?"^"+.@temp_color$+"(现有 "+.@item_count+" )":"" )+"^000000";
			}
		mes " ";
		mes "职业列表 :";
		if( .@base_job_bitmask ){
			deletearray .@available_job$;
			deletearray .@available_branch$;
			.@available_job_size = 0;
			.@available_branch_size = 0;
			for( .@i = 0; .@i < .base_job_size; .@i++ )
				if( .@base_job_bitmask & ( 1 << .@i ) ){
					.@class = roclass( .base_job[.@i] );
					.@available_job$[.@available_job_size] = jobname( .@class );
					if( .@check_progress & 8 && BaseClass == .@class )
						.@class_progress = 1;
					.@available_job_size++;
				}
			mes " ^777777"+implode( .@available_job$,"," )+" ^000000";	
			if( .@job_branch_bitmask && .@class_progress ){
				for( .@i = 0; .@i < .job_branch_size; .@i++ )
					if( .@job_branch_bitmask & ( 1 << .@i ) ){
						if( .@check_progress & 8 && ( .@eac & .job_branch[.@i] ) )
							.@branch_progress++;
						.@available_branch$[.@available_branch_size] = .job_branch_name$[.@i];
						.@available_branch_size++;
					}
				mes "限制 : ^777777"+implode( .@available_branch$,", " )+" ^000000";
			}
		}else{
			mes " ^FF0000-未设置-^000000";
		}
		
		mes " ";
		mes "奖励列表 : ";
		mes "^777777 - 基本经验 : "+( ( .@baseexp )? .@baseexp:"<none>" )+"^000000";
		mes "^777777 - 职业经验 : "+( ( .@jobexp )? .@jobexp:"<none>" )+"^000000";
		mes "^777777 - 现金点数 : "+( ( .@cash )? .@cash:"<none>" )+"^000000";
		mes "^777777 - Zeny : "+( ( .@zeny )? .@zeny:"<none>" )+"^000000";
		mes "奖励物品列表 : "+( ( .@reward_size )?"":"^777777<none>" )+"^000000";
		if( .@reward_size )
			for( .@i = 0; .@i < .@reward_size; .@i++ ){
				.@item_id = getelementofarray( getarg(16),.@i );
				.@item_type = getiteminfo( .@item_id,2 );
				mes " ^777777 - "+getelementofarray( getarg(17),.@i )+" x "+getitemname( .@item_id )+" "+( ( .@item_type == 4 || .@item_type == 5 )?"["+getitemslots( .@item_id )+"]":"" )+"^000000";
			}
		mes " ";
		
		mes "发布任务的NPC : "+( ( .@selected_npc_size )?"":"^FF0000-未设置-" )+"^000000";
		if( .@selected_npc_size ){
			if( .@selected_npc_size >= 2 )
				.@selected_npc$ = "|"+implode( getarg(18),"|" )+"|";
			else
				.@selected_npc$ = "|"+getarg(18)+"|";
			for( .@i = 0; .@i < .mission_npc_count; .@i++ )
				if( ( compare( "|"+.@selected_npc$+"|","|"+.@i+"|" ) ) ){
					getmapxy( .@map$,.@x,.@y,1,.npc_unique_list$[.@i] );
					.@sub_npc_name$ = "^777777(" +.@map$+ ") ";
					.@sub_npc_name$ = .@sub_npc_name$ + .npc_name_list$[.@i] + "^000000";
					mes " - "+.@sub_npc_name$;
				}
			mes " ";
		}
		if( .@time_update$ != "0000-00-00 00:00:00" && .@time_update$ != "" ){
			mes "最后发布日期:";
			mes "^777777"+.@time_update$+"^000000";
		}
		
		if( .@check_progress ){
			if( .@check_progress & 1 && !.@lv_progress ){
				dispbottom "[错误] 您的等级未达到该委托任务的需求.";
				.@check_result |= 1;
			}
			if( .@check_progress & 2 && .@monster_size != .@monster_progress ){
				dispbottom "[错误] 您的怪物击杀数量未达到该委托任务的需求.";
				.@check_result |= 2;
			}
			if( .@check_progress & 4 && .@item_size != .@item_progress ){
				dispbottom "[错误] 您收集的物品数量未达到该委托任务的需求.";
				.@check_result |= 4;
			}
			if( .@check_progress & 8 && ( !.@branch_progress && !.@class_progress ) ){
				dispbottom "[错误] 您的职业不符合该委托任务的需求.";
				.@check_result |= 8;
			}
		}
	return .@check_result;	
}


// the number behind the NPC name must be NUMBER with range of ( 1 ~ 500 )
// the number should stay the same for eternity, if you change it frequently, it might affect your missions for each NPC.
// ( to conclude, once you assigned the number, dont change it for the sake of your mission board ... )

prontera,151,171,4	duplicate(mission_board)	任务委托 A#1	2_BULLETIN_BOARD 
prontera,154,174,4	duplicate(mission_board)	任务委托 B#2	2_BULLETIN_BOARD 
prontera,156,176,4	duplicate(mission_board)	任务委托 C#3	2_BULLETIN_BOARD 
prontera,159,179,4	duplicate(mission_board)	任务委托 D#4	2_BULLETIN_BOARD 

prontera,151,161,4	duplicate(mission_board)	任务委托 E#5	2_BULLETIN_BOARD 
prontera,154,164,4	duplicate(mission_board)	任务委托 F#6	2_BULLETIN_BOARD 
prontera,156,166,4	duplicate(mission_board)	任务委托 G#7	2_BULLETIN_BOARD 
prontera,159,169,4	duplicate(mission_board)	任务委托 H#8	2_BULLETIN_BOARD 



