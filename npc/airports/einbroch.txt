//===== RoSF.us Script ======================================
//= Einbroch Airport Staff
//===== By: =========Translation:============================= 
//= L0ne_W0lf        安赫尔|20130701[ROSF.US], Muad_Dib
//===== Description: =========================================
//= Einbroch Airport Staff
//===== Additional Comments: =================================
//= Official NPCs.
//============================================================ 

airport,143,43,5	script	机场 工作人员#airport1a::airport1	4_F_01,{
	mes "[机场 工作人员]";
	mes "欢迎来到";
	mes "艾音布洛克机场, ";
	mes "这里我们提供前往";
	mes "朱诺, 里希塔乐镇以及毁葛的";
	mes "直达航班.";
	next;
	if (select("乘坐飞艇:取消") == 1) {
		mes "[机场 工作人员]";
		mes "坐飞艇的费用是";
		mes "1,200 zeny, 但是如果您有";
		mes "飞空艇乘坐免费券, ";
		mes "这笔费用可以不收.";
		mes "您要乘坐飞空艇吗?";
		next;
		if (select("是:否") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				end;
			}
			mes "[机场 工作人员]";
			mes "对不起, 但是您";
			mes "并没有飞空艇";
			mes "免费券, 同时您也";
			mes "没有足够的 zeny 来乘坐";
			mes "飞空艇.请记得";
			mes "乘坐价格为 1,200 zeny";
			close;
		}
	}
	mes "[机场 工作人员]";
	mes "谢谢!";
	mes "祝您愉快!";
	close;
}

airport,158,43,5	duplicate(airport1)	机场 工作人员#airport1b	4_F_01
airport,126,43,5	duplicate(airport1)	机场 工作人员#airport1c	4_F_01

airport,143,49,3	script	离港 工作人员#airport2a::airport2	4_F_01,{
	mes "[离港 工作人员]";
	mes "欢迎来到艾音布洛克机场.";
	mes "如果您已经抵达了您的目的地, ";
	mes "请允许我引导您前往主航站楼.";
	mes "如果不是的话, 请登上前往朱诺, 里希塔乐镇以及毁葛";
	mes "的飞空艇前往您的目的地.";
	next;
	if (select("前往主航站楼:取消") == 1) {
		mes "[离港 工作人员]";
		mes "一旦您前往了主航站楼, 要再次登机, ";
		mes "必须再次支付登记费.";
		mes "请您在离港前先确保";
		mes "您的目的地为艾音布洛克.";
		mes "是否前往主航站楼?";
		next;
		if (select("是:否") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[离港 工作人员]";
	mes "好的, 谢谢";
	mes "您的支持.";
	mes "同时我们衷心祝愿您";
	mes "旅途愉快~";
	close;
}

airport,126,51,3	duplicate(airport2)	离港 工作人员#airport2b	4_F_01
airport,158,50,3	duplicate(airport2)	离港 工作人员#airport2c	4_F_01

einbroch,94,267,3	script	飞空艇 工作人员#ein01	4_F_02,{
	mes "[飞空艇 工作人员]";
	mes "欢迎来到";
	mes "艾音布洛克机场.";
	mes "请通过这扇门前往";
	mes "登上驶向位于秀发兹发德共和国";
	mes "的朱诺、里希塔乐镇以及毁葛";
	mes "的飞空艇.";
	next;
	mes "[飞空艇 工作人员]";
	mes "不过, 如果艾音布洛克是";
	mes "您的目的地.请走下面的楼梯";
	mes "并请求离港工作人员引导您";
	mes "前往主航站楼.";
	mes "谢谢!";
	mes "并祝您旅途愉快.";
	close;
}
