//===== RoSF.us Script ======================================
//= Milk Trader
//===== By: ===============Translation:=======================
//= kobra_k88; L0ne_W0lf   安赫尔/Money[RoSF.us]20131006
//===== Current Version: =====================================
//= 1.5
//===== Description: =========================================
//= [Official Conversion]
//= Trades bottles for milk
//===== Additional Comments: =================================
//= Fully working
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Raised the price to close zeny exploit [Lupus]
//= 1.2a Switched to Lupus's "loopless" technique.[kobra_k88]
//= 1.3 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4 Implemented checkweight. [L0ne_W0lf]
//= 1.5 Fixed missed variable. (bugreport:1523) [L0ne_W0lf]
//============================================================

prontera,73,140,0	script	牛奶商人	4_M_04,{
	if (checkweight(1201,1) == 0) {
		mes "^3355FF等一下!";
		mes "我无法为您提供服务";
		mes "因为您身上的东西";
		mes "实在是太多了. ";
		mes "放一些东西到卡普拉仓库";
		mes "然后再回来找我吧~";
		close;
	}
	mes "[牛奶商人]";
	mes "嘿, 嘿……";
	mes "如果您给我";
	mes "1个空瓶与";
	mes "15 Zeny, 我就可以";
	mes "用它们给您换 1 瓶牛奶, ";
	mes "这听上去如何? ";
	next;
	if (select("兑换所有空瓶:取消") == 1) {
		if (countitem(713) <= 0) {
			mes "[牛奶商人]";
			mes "嘿……";
			mes "你一个空瓶";
			mes "也没有. ";
			mes "这样的话";
			mes "我实在没法给您";
			mes "牛奶, 你懂的……";
			close;
		}
		set .@bottles,countitem(713);
		set .@total_weight,.@bottles * 50;
		set .@total_cost,.@bottles * 15;
		if (Zeny < .@total_cost) {
			mes "[牛奶商人]";
			mes "噢, 哇~!";
			mes "您没有足够的";
			mes "zeny 来用这些";
			mes "空瓶换牛奶. ";
			mes "您需要有";
			mes "至少 " + .@total_cost + " zeny.";
			close;
		}
		if (MaxWeight - Weight < .@total_weight) {
			mes "[牛奶商人]";
			mes "嗯……";
			mes "您得先";
			mes "把您的背包";
			mes "清理出一些空间, ";
			mes "我才能给您";
			mes "这么多牛奶. ";
			close;
		}
		set Zeny, Zeny - .@total_cost;
		delitem 713,.@bottles; //空瓶
		getitem 519,.@bottles; //牛奶
		close;
	}
	close;
}
