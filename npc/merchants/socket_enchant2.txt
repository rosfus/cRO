//===== RoSF.us Script ======================================
//= Episode 12 Socket Enchant NPC
//===== By: ==================================================
//= Gepard
//===== Current Version: =====================================
//= 1.1a
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================

-	script	利亚布仁#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- 等一下 !! -";
		mes "- 您的负重太大 -";
		mes "- 请减少您的负重再来. -";
		close;
	}
	mes "[利亚布仁]";
	mes "真是一个好天气啊. 我的名字是利亚布仁,";
	mes "我是一名专门从事给武器和防具打洞的技术人员.";
	mes "有时候, 这项工作非常困难, 但是我很喜欢并以此为荣.";
	next;
	mes "[利亚布仁]";
	mes "赛亚布仁是我的哥哥, 我从他那里学到了很多.";
	mes "但是我想我还有很多要学习.";
	mes "所以和我哥哥不一样, 我的操作技能有限, 请理解.";
	next;
	mes "[利亚布仁]";
	mes "我哥哥并不是很乐意来教我,";
	mes "因为他觉的如果我学会了, 他就不是独一无二的了.";
	mes "但是, 他还是尽其所能来给予我帮助了.";
	next;
	mes "[利亚布仁]";
	mes "感谢我哥哥, 我学到了很多.";
	mes "尝试不同的生活, 会让我感觉不是那么自信.";
	mes "但是我还是会尽我最大的努力的.";
	next;
	mes "[利亚布仁]";
	mes "事实上, 有一件事情,";
	mes "我哥哥并不想让我尝试.";
	mes "我不想让他烦躁, 所以我们跳过这一部分吧.";
	mes "不要再说这个话题了!";
	next;
	mes "[利亚布仁]";
	mes "请问, 你是否有一些装备,";
	mes "想要我为你进行打洞吗?";
	mes "我所需的费用, 材料, 还有成功率,";
	mes "都取决于,";
	mes "我所打洞的装备级别.";
	next;
	switch(select("- 武器:- 防具:- 关于你不想说的那些内容:- 更多的信息:- 取消"))
	{
		case 1:
			mes "[利亚布仁]";
			mes "武器, 是吗? 好的没问题.";
			mes "武器被分为4个级别,";
			mes "取决于打洞的难易程度.";
			mes "C级最简单, S级最难.";
			mes "你想尝试哪一个级别的呢?";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[利亚布仁]";
					mes "C级的, 好的.";
					mes "那么, 你有什么武器呢?";
					next;
					switch(select("- 地灵之书:- 水灵之书:- 风灵之书:- 火灵之书"))
					{
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10;
					}
				case 2:
					mes "[利亚布仁]";
					mes "你选择的是B级的.";
					mes "那么, 你有什么武器呢?";
					next;
					switch(select("- 兽人斧:- 弯刀:- 暗盲之锤"))
					{
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1;
						case 3: callfunc "Func_Socket2",1523,1538,40,61,300,984,1;
					}
				case 3:
					mes "[利亚布仁]";
					mes "A级的吗, 你的欲望不小啊, 不过, 看我的吧.";
					mes "那么, 你有什么武器呢?";
					next;
					switch(select("- 天龙短剑:- 刺藤拳刃:- 火灵拳刃:- 冰灵拳刃:- 风灵拳刃:- 黄金之锤:- 琵琶:- 女王的鞭子:- 幻影长矛:- 加野矛:- 风灵之剑"))
					{
						case 1:  callfunc "Func_Socket2",13001,13030,40,61,500,984,2;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,61,500,984,2;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,61,500,984,2;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,61,500,984,2;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,56,500,984,2;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,56,500,984,2;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,61,500,984,2;
					}
				case 4:
					mes "[利亚布仁]";
					mes "啊, S级的, 你不是开玩笑吧?";
					mes "那么 ...... 你想给哪一件武器打洞呢?!";
					next;
					switch(select("- 风灵之矛:- 破甲短剑:- 龙牙剑:- 削刃短剑:- 妖短剑泣神怨:- 圣锤十字星:- 死神之剑"))
					{
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10;
					}
			}
		case 2:
			mes "[利亚布仁]";
			mes "防具吗? 没问题.";
			mes "防具被分为4个等级,";
			mes "取决于打洞的难易程度.";
			mes "C级最简单, S级最难.";
			mes "你想尝试哪一个级别的呢?";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[利亚布仁]";
					mes "C级的, 我知道了.";
					mes "那么, 你想为哪一件防具打洞呢?";
					next;
					switch(select("- 向日葵头饰:- 学士帽:- 红蝶发带:- 学生帽"))
					{
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5;
					}
				case 2:
					mes "[利亚布仁]";
					mes "你选择的是B级的.";
					mes "那么, 你想为哪一件防具打洞呢?";
					next;
					switch(select("- 骷髅戒指:- 抗魔鞋"))
					{
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5;
					}
				case 3:
					mes "[利亚布仁]";
					mes "A级的吗, 你的欲望不小啊, 不过, 看我的吧.";
					mes "那么, 你想为哪一件防具打洞呢?";
					next;
					switch(select("- 海盗头巾:- 黑皮靴"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1;
					}
				case 4:
					mes "[利亚布仁]";
					mes "啊, S级的, 你不是开玩笑吧";
					mes "那么 ...... 你想为哪一件防具打洞呢?!";
					next;
					switch(select("- 魔法外套:- 天衣月舞:- 大天使之盾:- 衬衫:- 三角裤"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1;
						case 4: callfunc "Func_Socket2",2522,2523,45,51,1000,985,1;
						case 5: callfunc "Func_Socket2",2339,2371,45,51,1000,985,1;
					}
			}
		case 3:
			mes "[利亚布仁]";
			mes "噢哈哈, 没有什么特别的,";
			mes "他不想和别的人混为一谈. ";
			mes "我不想让他心烦,";
			mes "所以我最好忘了吧.";
			next;
			mes "[利亚布仁]";
			mes "...那项操作本身具有很高的成功率,";
			mes "但是需要非常昂贵的材料和很高的服务费用. ";
			mes "我十分怀疑有人竟愿意使用这项服务,";
			mes "虽说我可以接这个活.";
			next;
			mes "[利亚布仁]";
			mes "...厄...";
			next;
			mes "[利亚布仁]";
			mes "恩......";
			next;
			mes "[利亚布仁]";
			mes "...呼...";
			next;
			mes "[利亚布仁]";
			mes "你看起来对这个很有兴趣.";
			mes "如果是这样的话, 我至少可以和你谈谈. ";
			mes "但是你必须要保密.";
			mes "如果我哥哥知道了我和你说了些什么,";
			mes "他会打我一顿的!";
			next;
			mes "[利亚布仁]";
			mes "这项操作的成功率是90%,";
			mes "但是需要2块黄金和2亿的zeny";
			mes "我可以为 [太阳神头盔] 打洞.";
			next;
			mes "[利亚布仁]";
			mes "我哥哥曾经说过, 从上古时代开始, 就严禁对那个头盔做什么手脚. ";
			mes "但我相信对于愈是严禁的事物,";
			mes "人们就愈会产生很大的好奇感.";
			mes "我只是不想被我哥哥大骂一顿, 仅此而已.";
			next;
			switch(select("进行这项操作","取消"))
			{
				case 1:
					mes "[利亚布仁]";
					mes ".................";
					next;
					mes "[利亚布仁]";
					mes ".................";
					next;
					mes "[利亚布仁]";
					mes "...什么... 你说什么? 不!";
					mes "即使你鄙视我,";
					mes "我也不会做的!";
					mes "不, 我说了不! 不做就是不做! 不!";
					next;
					mes "[利亚布仁]";
					mes "不, 我不会做的! 不!";
					next;
					mes "[利亚布仁]";
					mes "我不能这样做...";
					next;
					mes "[利亚布仁]";
					mes "我不能...这么做...";
					next;
					mes "[利亚布仁]";
					mes ".................";
					next;
					mes "[利亚布仁]";
					mes "啊...";
					next;
					mes "[利亚布仁]";
					mes "你太固执了, 好吧, 下不为例.";
					mes "千万别告诉任何人,";
					mes "特别是我的哥哥, 你能发誓吗?";
					next;
					mes "[利亚布仁]";
					mes "好吧, 请给我";
					mes "^FF00002块黄金, 2亿zeny -- 服务费用 --,";
					mes "^FF0000还有太阳神头盔.";
					mes "^FF0000你要知道, 你的成功率是90%, 知道吗?^000000";
					mes "我们没有多余的时间了,";
					mes "所以, 让我们尽快做成这笔生意吧.";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[利亚布仁]";
						mes "可以开始了吗?";
						mes "上帝保佑.";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[利亚布仁]";
							mes "耶! 我就知道90%是妥妥的!";
							mes "看吧, 这个卡槽被完美的镶嵌进去了.";
							mes "恭喜你.";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							getitem 5353,1; //Helm_Of_Sun_
							next;
							mes "[利亚布仁]";
							mes "这是我第一次也是最后一次做这个事情.";
							mes "不要告诉任何人,";
							mes "好不好?";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[利亚布仁]";
							mes "啊! 失败了, 天啊!";
							mes "老天啊, 我想我们的运气差到了极点...";
							mes "我们该怎么办? ...";
							mes "这就是一开始我拒绝你的原因了!";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							next;
							mes "[利亚布仁]";
							mes "唉, 不要让我再做这么冒险的事情了!";
							mes "呸~";
							close;
						}
					}
					else
					{
						mes "[利亚布仁]";
						mes "厄...";
						next;
						mes "[利亚布仁]";
						mes "你没带够材料啊.";
						mes "骚年, 这就让我来为你办这个事儿啊?";
						mes "我不会再为你冒险了,";
						mes "即使你带够了材料. 呸~";
						close;
					}
				case 2:
					mes "[利亚布仁]";
					mes "不要把我今天跟你讲的,";
					mes "再告诉任何人, 好吗?";
					close;
			}
		case 4:
			mes "[利亚布仁]";
			mes "你还想了解的更多吗?";
			mes "嗯, 我想你不必要了解的太多.";
			mes "但是至少我可以给你一些提示.";
			next;
			mes "[利亚布仁]";
			mes "不同城市的打洞工程师,";
			mes "会有不同的打洞成功率.";
			mes "这取决于你的装备的精炼值.";
			mes "我不知道这是不是真的,";
			mes "但是我能感觉到不同城市的差别.";
			next;
			mes "[利亚布仁]";
			mes "举个例子, 某个城市的工程师,";
			mes "对精炼值较高的装备打洞成功率高";
			mes "但是在另一些城市,";
			mes "则正好相反.";
			next;
			mes "[利亚布仁]";
			mes "如果打洞失败了,";
			mes "装备就会损毁.";
			mes "打洞是一件非常复杂的操作,";
			mes "但是, 我会竭尽所能.";
			next;
			mes "[利亚布仁]";
			mes "噢, 有一件重要的事情我必须要提醒你.";
			mes "^FF0000打洞的时候,";
			mes "^FF0000不要在物品栏里放置和想要打洞的装备一样名字的装备.";
			mes "^FF0000比如, 你要给未精炼的斗篷打洞, 物品栏里却放了一件+9的斗篷.";
			mes "^FF0000我会随机从中选一件进行打洞操作,";
			mes "^FF0000如果失败的话, 你有可能会损失一件对你更重要的装备.^000000";
			next;
			mes "[利亚布仁]";
			mes "希望我说的这些对你有所帮助.";
			mes "谢谢.";
			close;
		case 5:
			mes "[利亚布仁]";
			mes "那么, 就是这些了.";
			close;
	}
}

payon,236,199,3	duplicate(SocketEnchant2)	利亚布仁#pay	4_M_04
lighthalzen,96,137,3	duplicate(SocketEnchant2)	利亚布仁#lhz	4_M_04
prt_in,22,60,6	duplicate(SocketEnchant2)	利亚布仁#prt	4_M_04
moc_ruins,154,86,3	duplicate(SocketEnchant2)	利亚布仁#moc	4_M_04

function	script	Func_Socket2	{

	disable_items;
	mes "[利亚布仁]";
	if(getarg(7,0) != 0 && getarg(8,0) != 0)
	{
		mes getitemname(getarg(0)) + "... 好了, 你需要给我";
		mes "^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 Zeny 作为服务费,";
		mes "^FF0000" + getarg(6) + "个 " + getitemname(getarg(5)) + " 和 " + getarg(8) + "个 " + getitemname(getarg(7)) + ".";
		mes "^FF0000当然了, 记的带给我 " + getitemname(getarg(0)) + ".^000000";
	}
	else
	{
		mes getitemname(getarg(0)) + "? 好了, 你需要给我";
		mes "^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 Zeny 作为服务费 还有 " + getarg(6) + " " + getitemname(getarg(5)) + (getarg(6) > 1 && getarg(5) != 999 ? "s":"") + " 作为必须条件.";
		mes "^FF0000当然了, 记的带给我 " + getitemname(getarg(0)) + ".";
		mes "^FF0000必须把所需的都带来.^000000";
	}
	next;
	mes "[利亚布仁]";
	mes "你已经把所需物品都带来了吗?";
	mes "给你一个忠告,";
	mes "所需的材料都会消耗掉, 当然也包括 " + (getiteminfo(getarg(0),5)&2 ? "武器" : "防具") + "."; // EQP_HAND_R = 2, it's a weapon, otherwise armor
	mes "还有一点, 如果 " + (getiteminfo(getarg(0),5)&2 ? "武器" : "防具") + " 是精炼过的, 或是插了卡片的,";
	mes "即使打洞成功了, 精炼值和卡片也会消失.";
	next;
	switch(select("- 开始打洞吧.:- 下次再说."))
	{
		case 1:
			if ((Zeny >= getarg(4)*1000) && (countitem(getarg(5)) >= getarg(6)) && (countitem(getarg(7,512)) >= getarg(8,0)) && (countitem(getarg(0)) > 0)) // if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[利亚布仁]";
				mes "好吧, 让我们开始吧!";
				mes "你最好祈祷有一个好的结果.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect EF_LORD;
					mes "[利亚布仁]";
					mes "太棒了, 看起来我成功了.";
					mes "真是不错的作品, 恭喜你了!";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					getitem getarg(1),1;
					next;
					mes "[利亚布仁]";
					mes "再见了, 亲!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[利亚布仁]";
					mes "呀! ...对不起, 我失败了.";
					mes "但是, 我是无辜的啊.";
					mes "这是你的运气不好, 上帝说了算的, 对不对?";
					mes "不要这么失望嘛,";
					mes "下次再来吧.";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					next;
					mes "[利亚布仁]";
					mes "再见了, 亲!";
					close;
				}
			}
			else
			{
				mes "[利亚布仁]";
				mes "你是呆萌吗? 你并没有把我需要的东西全部带来!";
				mes "快点把它们拿来!";
				close;
			}
		case 2:
			mes "[利亚布仁]";
			mes "下次再见了.";
			close;
	}
}
