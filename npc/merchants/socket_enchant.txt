//===== RoSF.us Script ======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 2.0a
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed OnTouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//============================================================

-	script	赛亚布仁#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- 等一下 !! -";
		mes "- 您的负重太大 -";
		mes "- 请减少您的负重再来. -";
		close;
	}
	mes "[赛亚布仁]";
	mes "我是一位给普通武器和防具打洞的工程师.";
	mes "我打洞的费用取值, 参照所需的材料和装备的不同而有所不同.";
	next;
	mes "[赛亚布仁]";
	mes "打洞看起来挺简单, 实际上非常困难.";
	mes "如果你有兴趣的话, 我们再聊.";
	next;
	switch(select("- 给武器打洞:- 给防具打洞:- 关于打洞:- 取消"))
	{
		case 1:
			mes "[赛亚布仁]";
			mes "你想给武器打洞吗?";
			mes "武器打洞有困难等级, C级最简单, S级最难.";
			mes "你想尝试哪一个级别的呢?";
			next;
			switch(select("- C级:- B级:- A级:- S级"))
			{
				case 1:
					mes "[赛亚布仁]";
					mes "C级, 是吗?";
					mes "目前, 我只能给这个级别的部分武器打洞, 那么, 从下面选择你要打洞的武器吧.";
					next;
					switch(select("- 德瑞尹长戟:- 鞭子:- 小提琴"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[赛亚布仁]";
					mes "B级?";
					mes "好吧, 这是适中困难级别的.";
					mes "选择你想要打洞的武器吧.";
					next;
					switch(select("- 链锤:- 长柄短剑:- 角弓:- 步兵长矛:- 海东剑:- 鲁特琴:- 钢铁鞭子:- 虎爪拳套:- 埃布尔雷弓"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[赛亚布仁]";
					mes "噢, A级的啊.";
					mes "这次的风险可不小啊!";
					mes "好吧, 你想要给哪件武器打洞呢?";
					next;
					switch(select("- 猎人之弓:- 高等生存的魔杖:- 巨大双手剑:- 水纹之剑:- 刺杀拳刃:- 贝利斯塔强弩:- 昏迷之锤:- 秘拳套:- 十字巨剑"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[赛亚布仁]";
					mes "哇...哇...哇噢...";
					mes "S级的吗? 好吧...";
					mes "如果我能成功的话, 简直就是一个奇迹.";
					mes "好吧... 你要给哪一把武器打洞呢?";
					next;
					switch(select("- 刚尼尔长矛:- 巨毒短剑:- 破甲短锥:- 砂尘短剑:- 卡卡普剑:- 狂击之剑:- 死神之镰:- 生存的魔杖"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[赛亚布仁]";
			mes "你说是, 防具?";
			mes "没问题.";
			mes "根据难易程度, 防具打洞分为4个困难级别.";
			mes "C级最简单, S级最难.";
			mes "想要做哪一个级别的?";
			next;
			switch(select("- C级:- B级:- A级:- S级"))
			{
				case 1:
					mes "[赛亚布仁]";
					mes "C级的, 是吗?";
					mes "听起来比较可行.";
					mes "你想给哪一件防具打洞?";
					next;
					switch(select("- 大衣:- 长大衣:- 头箍:- 圣职之帽"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[赛亚布仁]";
					mes "你选择的是B级.";
					mes "那么, 你选择的是哪一件防具?";
					next;
					switch(select("- 镜盾:- 钢铁锁子甲:- 圣职之衣:- 丝质外袍:- 战士长靴:- 长靴:- 披肩:- 铁盾:- 圆盾:- 钢盾:- 妖道帽"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[赛亚布仁]";
					mes "噢, A级的啊.";
					mes "这可是有不小风险的啊!";
					mes "好吧, 你想给哪一件打洞呢?";
					next;
					switch(select("- 宝石头盔:- 软呢帽:- 谎言纪录本:- 紧身便衣:- 秘银金属铠甲:- 钢铁铠甲:- 盗贼之衣:- 重靴:- 修女的帽子:- 斗篷:- 金属头盔:- 忍服血樱:- 兽人头盔:- 伯爵斗篷:- 神圣之帽:- 黄金帽:- 敏捷别针:- 僵尸帽"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[赛亚布仁]";
					mes "啊... S级的啊?";
					mes "你确定吗?";
					mes "好吧... 先让我看看你要给哪件打洞, 我会告诉你我需要什么.";
					next;
					switch(select("- 山羊头盔:- 尖角发圈:- 骨制头盔:- 海盗船长帽:- 王冠:- 后冠:- 人面狮身帽:- 创作者外袍:- 智力耳环:- 力量戒指:- 神射手套"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[赛亚布仁]";
			mes "嗯, 我还没有掌握最精湛的打洞技术.";
			mes "这项技术太复杂了, 如果我告诉你我已熟悉影响打洞成功率的所有条件的话, 我一定是在骗你.";
			mes "但是, 我还是可以给你一些提示...";
			next;
			mes "[赛亚布仁]";
			mes "在有些城市里, 精炼值低的装备打洞成功率高.";
			mes "而在另外一些城市, 则正好相反.";
			mes "是不是很奇怪啊?";
			next;
			mes "[赛亚布仁]";
			mes "有一些非常重要的事情你必须要知道.";
			mes "你如果要我打洞的话, 不要在物品栏里放置同样的一件装备.";
			next;
			mes "[赛亚布仁]";
			mes "举个例子: 如果你的物品栏里有一件+7的斗篷和一件未精炼的斗篷, 我会随机从中选一件进行打洞操作.";
			mes "请务必谨慎.";
			next;
			mes "[赛亚布仁]";
			mes "再强调一次, ^FF0000把想要打洞的装备只留一件放在物品栏内^000000.";
			mes "所有相同名字的装备都不要放在身上, 明白了?";
			close;
		case 4:
			mes "[赛亚布仁]";
			mes "不急, 冒险家.";
			mes "如果你想要给某件装备打洞的话, 就来找我吧.";
			mes "再见了~";
			close;
	}
}

function	script	Func_Socket	{

	disable_items;
	mes "[赛亚布仁]";
	mes "你要给 " + getitemname(getarg(0)) + " 打洞吗?";
	mes "好吧, 请给我 ^FF0000" + getarg(6) + " " + getitemname(getarg(5)) + (getarg(6) > 1 && getarg(5) != 999 ? "s":"") + "^000000, " + (getarg(7,0) != 0 && getarg(8,0) != 0 ? ("^FF0000" + getarg(8,0) + " " + getitemname(getarg(7,0)) + (getarg(8,0) > 1 && getarg(7,0) != 999 ? "s":"") + "^000000 ") : "") + "and my ^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 Zeny^000000 服务费.";
	mes "嗯, 还有别忘了 " + getitemname(getarg(0)) + "!";
	next;
	mes "[赛亚布仁]";
	if (getiteminfo(getarg(0),5)&2) // EQP_HAND_R = 2, it's a weapon
	{
		mes "如果你东西带齐了的话, 我可以开始打洞了.";
		mes "但是你必须知道, 我是有可能会失手的.";
		mes "所以, 我要给你提个醒...";
		next;
		mes "[赛亚布仁]";
		mes "如果我操作失败了, 那么 ^FF0000武器^000000 和 ^FF0000武器上的卡片^000000 都会消失.";
	}
	else // armor
	{
		mes "如果你的装备, 我所需的材料和服务费都带齐了的话, 我们就可以开始了.";
		mes "但是在这之前, 我必须提醒你, 这是有风险的.";
		next;
		mes "[赛亚布仁]";
		mes "如果打洞失败, 那么 ^FF0000防具^000000 和 ^FF0000防具上的卡片^000000 都会消失.";
	}
	mes "你仍旧要给装备打洞吗?";
	next;
	switch(select("是的:取消"))
	{
		case 1:
			if ((Zeny >= getarg(4)*1000) && (countitem(getarg(5)) >= getarg(6)) && (countitem(getarg(7,512)) >= getarg(8,0)) && (countitem(getarg(0)) > 0)) // if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[赛亚布仁]";
				mes "那么, 让我开始工作吧!";
				mes "你最好祈祷有一个好的结果.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[赛亚布仁]";
					mes "太棒了, 看起来你成功了.";
					mes "真是相当不错啊, 祝贺你了!";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					getitem getarg(1),1;
					next;
					mes "[赛亚布仁]";
					mes "再见了, 亲!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[赛亚布仁]";
					mes "哇! ...对不起啊, 我失败了.";
					mes "但是, 这并不是我的错.";
					mes "这是你的运气不好罢了, 上帝说了算的, 对不对?";
					mes "别这么泄气嘛, 下次再来.";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					next;
					mes "[赛亚布仁]";
					mes "祝你下次好运了!";
					close;
				}
			}
			else
			{
				mes "[赛亚布仁]";
				mes "如果你把我所需的带齐了的话, 我很乐意为你效劳.";
				mes "你确定带来了所需的装备, 材料, 金钱?";
				close;
			}
		case 2:
			mes "[赛亚布仁]";
			mes "还需要再想一想, 是吗?";
			mes "好的, 我能理解.";
			mes "要记住, 不尝试冒险的话, 生活就太无味了~";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	赛亚布仁#pay	4_M_02
lhz_in02,281,35,5	duplicate(SocketEnchant)	赛亚布仁#lhz	4_M_02
prt_in,33,70,5	duplicate(SocketEnchant)	赛亚布仁#prt	4_M_02
morocc,51,41,4	duplicate(SocketEnchant)	赛亚布仁#moc	4_M_02

-	script	Young Man#dummy::SocketMan	-1,{
	mes "[年青人]";
	mes "我正在考虑成为一名打洞工程师.";
	mes "给装备打洞是一个全新的领域, 而且我通过这个职业能赚到不少的钱.";
	next;
	mes "[年青人]";
	mes "据我所知, 要成功给装备打洞, 需要太多运气的成份.";
	mes "你也许会失败几次, 不过你足够幸运的话, 你可以给装备一次打出两个洞.";
	mes "很疯狂, 不是吗?";
	next;
	mes "[年青人]";
	mes "但是看起来你不是很兴奋的样子.";
	mes "只有固定的一些装备可以打出额外的两个洞.";
	mes "即使我要做一名学徒, 我还是要学习很多很多...";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	年轻人#prt	4W_M_01
payon,143,143,7	duplicate(SocketMan)	年轻人#pay	4W_M_01
morocc,60,42,3	duplicate(SocketMan)	年轻人#moc	4W_M_01
lhz_in02,269,33,5	duplicate(SocketMan)	年轻人#lhz	4W_M_01
