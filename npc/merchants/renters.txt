//===== RoSF.us Script ======================================
//= Renters
//===== By: =========Translation:============================= 
//= kobra_k88        安赫尔|20130701[ROSF.US], mod by Lupus
//===== Current Version: =====================================
//= 2.2
//===== Description: =========================================
//= Knight and Crusader Peco Peco Breeders, Falcon Breeder scripts
//===== Additional Comments: =================================
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7 Updated to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.8 Added 3rd Job creature NPCs (Dragon/Gryphon) [L0ne_W0lf]
//= 1.9 Enabled Gryphon Renter NPC and added Mado Gear NPC. [Masao]
//= 2.0 Fixed problems with third classes and new mounts. [Euphy]
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Added warg checks. [Euphy]
//============================================================

// PecoPeco Breeder (for Knights)-------------------------------------------
prontera,55,350,5	script	大嘴鸟 管理兵#knt	8W_SOLDIER,{
	if(Upper==0) set .@price,2500; //Normal Peco - default price
	if(Upper==1) set .@price,2500; //Armored Peco
	if(Upper==2) set .@price,2500; //Baby Peco

	mes "[大嘴鸟 管理兵]";
	if (BaseJob == Job_Knight && Class < Job_Rune_Knight) {
		mes "欢迎您. ";
		mes "尊敬的骑士, ";
		mes "您是不是想要租用";
		mes "一直大嘴鸟? 租用";
		mes "费用为 "+.@price+" zeny. ";
		next;
		switch(select("租用大嘴鸟:取消")) {
		case 1:
			if (Zeny < .@price) {
				mes "[大嘴鸟 管理兵]";
				mes "您没有";
				mes "足够的zeny. ";
				mes "您是不是……";
				mes "破产了? ";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[大嘴鸟 管理兵]";
				mes "对不起. 但是您没有";
				mes "能力来使用这项服务. ";
				mes "请先学习";
				mes "骑乘术再来. ";
				close;
			}
			else if(checkriding()) {
				mes "[大嘴鸟 管理兵]";
				mes "您已经";
				mes "拥有一只";
				mes "大嘴鸟了. ";
				close;
			}
			else if(ismounting()) {
				mes "[大嘴鸟 管理兵]";
				mes "请先解除骑乘. ";
				close;
			}
			set Zeny, Zeny - .@price;
			setriding;
			close;
		case 2:
			mes "[大嘴鸟 管理兵]";
			mes "我明白了. ";
			mes "那么, ";
			mes "祝您愉快. ";
			close;
		}
	} else {
		mes "对不起, 但是";
		mes "这些大嘴鸟";
		mes "只提供给骑士以及";
		mes "骑士领主. ";
		close;
	}
}

// Grand PecoPeco Breeder (for Crusaders)-------------------------------------------
prontera,232,318,3	script	大嘴鸟 管理兵#cru	8W_SOLDIER,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[大嘴鸟 管理兵]";
	if (BaseJob == Job_Crusader && Class < Job_Rune_Knight) {
		if(Upper != 1 ) mes "欢迎, 十字军. ";
		else mes "欢迎您, 圣殿十字军. ";
		mes "我们为您准备了";
		mes "一只特殊的大嘴鸟";
		mes "租用这只大嘴鸟";
		mes "需要花费 "+.@price+" zeny.";
		next;
		switch(select("租用大嘴鸟:退出")) {
		case 1:
			if (Zeny < .@price) {
				mes "[大嘴鸟 管理兵]";
				mes "您没有足够的";
				mes "Zeny 来租用. ";
				mes "如果您想要一只";
				mes "大嘴鸟请";
				mes "带来" +.@price+" Zeny. ";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[大嘴鸟 管理兵]";
				mes "您必须首先学习";
				mes "骑乘术. ";
				mes "我才能租给您. ";
				close;
			}
			else if(checkriding()) {
				mes "[大嘴鸟 管理兵]";
				mes "您已经拥有";
				mes "一只大嘴鸟了. ";
				close;
			}
			else if(ismounting()) {
				mes "[大嘴鸟 管理兵]";
				mes "请先解除骑乘. ";
				close;
			}
			set Zeny, Zeny - .@price;
			setriding;
			close;
		case 2:
			mes "[大嘴鸟 管理兵]";
			mes "再见! ";
			close;
		}
	} else {
		mes "我能为您做什么? ";
		mes "请注意, 我们这里";
		mes "只提供给十字军与";
		mes "圣殿十字军大嘴鸟";
		mes "租赁服务. ";
		close;
	}
}

// Falcon Master--------------------------------------------------------------
hu_in01,381,304,5	script	猎鹰 管理员#hnt	8W_SOLDIER,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[猎鹰 管理员]";
	if (BaseJob == Job_Hunter) {
		if (checkoption(Option_Wug) || checkoption(Option_Wugrider)) {
			mes "唔……";
			mes "您先要解散座狼";
			mes "才能租用猎鹰! ";
			close;
		}
		mes "您需要猎鹰吗? ";
		mes "您可以租到一只";
		mes "忠实的猎鹰, 只需要";
		mes "付"+.@price+" zeny~";
		next;
		switch(select("租猎鹰:取消")) {
		case 1:
			if (checkoption(Option_Wug) || checkoption(Option_Wugrider)) {
				mes "[猎鹰 管理员]";
				mes "唔……";
				mes "您需要先解散座狼";
				mes "才能租用猎鹰. ";
				close;
			}
			if (Zeny < .@price) {
				mes "[猎鹰 管理员]";
				mes "这是什么? ";
				mes "您没有足够的";
				mes "Zeny?!";
				mes "您最好先去";
				mes "赚点钱, 而不是";
				mes "打猎~";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[猎鹰 管理员]";
				mes "天啊~";
				mes "先去学会";
				mes "怎么管理一只";
				mes "猎鹰! 如果您";
				mes "不知道怎么管理它";
				mes "我是无法租借给您的. ";
				close;
			}
			else if(checkfalcon()) {
				mes "[猎鹰 管理员]";
				mes "唔……";
				mes "您已经拥有了";
				mes "一只猎鹰. 它就在";
				mes "那里, 您看见了么? ";
				close;
			}
			set Zeny, Zeny - .@price;
			setfalcon;
			close;
		case 2:
			mes "[猎鹰 管理员]";
			mes "等等等……您";
			mes "想去哪里? 这些";
			mes "猎鹰是最好的! ";
			mes "我保证! 来吧~";
			close;
		}
	} else {
		mes "笨蛋! ";
		mes "猎鹰只能给";
		mes "猎人与神射手使用, ";
		mes "明白了吗? ";
		mes "……呵呵呵, 嫉妒么? ";
		close;
	}
}